
*Scene_Init_Result

	//ムービーの再生開始
	PlayMovieToGraph hdximg(ibg_Result),DX_PLAYTYPE_LOOP
	BASS_ChannelSetAttribute hbasssnd(bgmResult),BASS_ATTRIB_VOL,tofloat(0.4)
	BASS_ChannelSetPosition hbasssnd(bgmResult),0,0,0
	BASS_ChannelPlay hbasssnd(bgmResult)

	//スコア数値の描画
	if hdximg(iTx_Result_Score)!0 :DeleteGraph hdximg(iTx_Result_Score)
	hdximg(iTx_Result_Score)=MakeScreen(256,64,TRUE)
	SetDrawScreen hdximg(iTx_Result_Score)
		ChangeFontType DX_FONTTYPE_ANTIALIASING_4X4
		ChangeFont"Pier Sans":SetFontSize 50
		SetFontThickness 2
		DrawString 0,0,strf("%08d",curScore),GetColor(30,30,30)

	//ランクの判別
		dim ScoreRankID:ScoreRankID=RANK_ID_D
		if curScore>=RANK_SCORE_C :ScoreRankID=RANK_ID_C
		if curScore>=RANK_SCORE_B :ScoreRankID=RANK_ID_B
		if curScore>=RANK_SCORE_A :ScoreRankID=RANK_ID_A
		if curScore>=RANK_SCORE_AA :ScoreRankID=RANK_ID_AA
		if curScore>=RANK_SCORE_AAA :ScoreRankID=RANK_ID_AAA
		if curScore>=RANK_SCORE_S :ScoreRankID=RANK_ID_S
		if curScore>=RANK_SCORE_SS :ScoreRankID=RANK_ID_SS
		if curScore>=RANK_SCORE_SSS :ScoreRankID=RANK_ID_SSS

	//ランキング
		sdim tmpstr
		tmpstr=SERVICE_HTTP+"/API-SendGlobalRanking.php?key="+APIkey_SendGlobalRanking+"&id="+Libarr_ChartID(CursorLevID,CursorSelID)+"&name="+aqName+"&score="+curScore
		switch WebAPI_PostandGet(tmpstr,sWebAPI)
			case"ERROR_UNKNOWN_PRM"
				AddDebugConsole STYLE_WARNING,"Portal WebAPI: 無効なパラメータが指定されました。"
			swbreak
			case"ERROR_AUTH_APIKEY"
				AddDebugConsole STYLE_WARNING,"Portal WebAPI: APIキーの認証に失敗しました。"
			swbreak
		swend
	//スコア送信
		//グラフデータの初期化
			sdim tmpstr:sdim GraphBin,270
			repeat 270
				poke GraphBin,cnt,chartCaptureVal(cnt)+1	//NULL回避
			loop
			b64encode GraphBin,GraphBin
		//URL作成&リクエスト発行
			tmpstr=SERVICE_HTTP+"/API-SendNewRecord.php?key="+APIkey_SendNewRecord
			tmpstr+"&id="+Libarr_ChartID(CursorLevID,CursorSelID)
			tmpstr+"&name="+aqName
			tmpstr+"&score="+curScore
			tmpstr+"&j_ex="+numExcellent
			tmpstr+"&j_gr="+numGreat
			tmpstr+"&j_ni="+numNice
			tmpstr+"&j_ba="+numBad
			tmpstr+"&j_mi="+numMiss
			tmpstr+"&j_ea="+numEarly
			tmpstr+"&j_la="+numLate
			tmpstr+"&maxcombo="+curMaxCombo
			tmpstr+"&graph="+GraphBin
			switch WebAPI_PostandGet(tmpstr,sWebAPI)
				case"ERROR_UNKNOWN_PRM"
					AddDebugConsole STYLE_WARNING,"Portal WebAPI: 無効なパラメータが指定されました。"
				swbreak
				case"ERROR_AUTH_APIKEY"
					AddDebugConsole STYLE_WARNING,"Portal WebAPI: APIキーの認証に失敗しました。"
				swbreak
			swend

	//BGM停止
	BASS_ChannelStop hplaybgm

	return

*Scene_Result

	//決定キー判定
	if CheckHitKey(KEY_INPUT_RETURN)==TRUE {
		decideFlag=TRUE
	}

	//背景
		SetDrawBlendMode DX_BLENDMODE_NOBLEND,256
		DrawGraph 0,0,hdximg(ibg_Result),FALSE

	//ナビゲーション
		if decideFlag==FALSE {
			setease 0,256,ease_cubic_out
			EaseAlpha=getease(SceneCount-30,20)
			setease -32,0,ease_cubic_out
			EasePosX=getease(SceneCount-30,20)
		} else {
			setease 256,0,ease_cubic_out
			EaseAlpha=getease(decideFrameCount-20,10)
			setease 0,-32,ease_cubic_out
			EasePosX=getease(decideFrameCount-20,10)
		}
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,EaseAlpha
		DrawGraph 32+EasePosX,16,hdximg(iNav_Result),TRUE

	if SceneCount>10 {
		//左パネル背景
			setease 0,256,ease_cubic_out
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount-20,25)
			setease -128,0,ease_cubic_out
			DrawGraph getease(SceneCount-10,25),170,hdximg(ibg_Result_Info),TRUE

		//右パネル背景
			setease 0,256,ease_cubic_out
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount-20,25)
			setease 640+128,640,ease_cubic_out
			DrawGraph getease(SceneCount-15,25),0,hdximg(ibg_Result_Score),TRUE
	}
	if SceneCount>20 {
		//左パネルレイヤー
			setease 0,256,ease_cubic_out
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount-30,25)
			setease -128,0,ease_cubic_out
			DrawGraph getease(SceneCount-20,25),170,hdximg(iLayer_Result_Info),TRUE
		//右パネルレイヤー
			setease 0,256,ease_cubic_out
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount-30,25)
			setease 640+128,640,ease_cubic_out
			DrawGraph getease(SceneCount-25,25),0,hdximg(iLayer_Result_Score),TRUE
	}

	if SceneCount>30 {
		//左パネル
			setease 0,256,ease_cubic_out
			SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(SceneCount-30,10)

			//曲名
				sdim tmpstr:tmpstr=chartTitle
				DrawStringToHandle 40,194,strmid(tmpstr,0,limit(SceneCount-30,0,strlen(tmpstr))),GetColor(10,10,10),hdxfont(fNoto_m24)
			//アーティスト名
				sdim tmpstr:tmpstr=chartArtist
				DrawStringToHandle 40,234,strmid(tmpstr,0,limit(SceneCount-40,0,strlen(tmpstr))),GetColor(25,25,25),hdxfont(fNoto_m16)
			//難易度（文字列）
				DrawStringToHandle 410,205,chartDifficulty,GetDifficultyColor(chartDifficulty),hdxfont(fGood_14)
			//難易度（数値）
				DrawStringToHandle 510,205,"Lv."+str(chartLevel),GetDifficultyColor(chartDifficulty),hdxfont(fGood_14)

			setease 0,256,ease_cubic_out
			SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(SceneCount-60,20)
			setease -32,0,ease_cubic_out
			EasePosX=getease(SceneCount-60,20)

			//TOTAL NOTES
				setease 0,hitobjMax,ease_cubic_out
				DrawStringToHandle 182+EasePosX,290,strf("%04d",getease(SceneCount-60,25)),GetColor(30,30,30),hdxfont(fPier_m20)
			//BPM
				setease 0,Libarr_DefBPM(CursorLevID,CursorSelID),ease_cubic_out
				DrawStringToHandle 182+EasePosX,317,strf("%04d",getease(SceneCount-60,25)),GetColor(30,30,30),hdxfont(fPier_m20)
				if Libarr_DefBPM(CursorLevID,CursorSelID)!Libarr_MinBPM(CursorLevID,CursorSelID)&Libarr_DefBPM(CursorLevID,CursorSelID)!Libarr_MaxBPM(CursorLevID,CursorSelID) {
					DrawStringToHandle 182+EasePosX,345,strf("%04d - %04d",str(Libarr_MinBPM(CursorLevID,CursorSelID)),str(Libarr_MaxBPM(CursorLevID,CursorSelID))),GetColor(50,50,50),hdxfont(fPier_r14)
				}
			//クリアの種類
				sdim tmpstr
				if stgClearType==ID_Failed :tmpstr="FAILED"
				if stgClearType==ID_Clear {
					switch GaugeType
						case Gauge_Assist:tmpstr="EASY":swbreak
						case Gauge_Normal:tmpstr="NORMAL":swbreak
						case Gauge_Hard:tmpstr="HARD":swbreak
						case Gauge_Madness:tmpstr="MADNESS":swbreak
					swend
				}
				if stgClearType==ID_Fullcombo :tmpstr="FULL COMBO"
				if stgClearType==ID_Allex :tmpstr="ALL EXCELLENT"
				DrawStringToHandle 182+EasePosX,376,tmpstr,GetColor(30,30,30),hdxfont(fPier_m16)
			//最大コンボ
				setease 0,curMaxCombo,ease_cubic_out
				DrawStringToHandle 182+EasePosX,401,str(getease(SceneCount-60,25)),GetColor(30,30,30),hdxfont(fPier_m20)
			}

		//右パネル
			//スコア表示
				setease 0,256,ease_cubic_out
				SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(SceneCount-50,25)
				setease 128,0,ease_cubic_out
				DrawGraph 900+getease(SceneCount-50,25),190,hdximg(iTx_Result_Score),TRUE

			//判定別表示
				repeat 7
					setease 0,256,ease_cubic_out
					SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(SceneCount-70-cnt*3,25)
					setease 32,0,ease_cubic_out
					EasePosX=getease(SceneCount-70-cnt*3,25)

					switch cnt
						case 0:DrawStringToHandle 939+EasePosX,301,strf("%04d",numExcellent),GetColor(10,10,10),hdxfont(fPier_m20):swbreak		//EXCELLENT
						case 1:DrawStringToHandle 939+EasePosX,337,strf("%04d",numGreat),GetColor(10,10,10),hdxfont(fPier_m20):swbreak		//GREAT
						case 2:DrawStringToHandle 939+EasePosX,373,strf("%04d",numNice),GetColor(10,10,10),hdxfont(fPier_m20):swbreak			//NICE
						case 3:DrawStringToHandle 939+EasePosX,409,strf("%04d",numBad),GetColor(10,10,10),hdxfont(fPier_m20):swbreak			//BAD
						case 4:DrawStringToHandle 939+EasePosX,445,strf("%04d",numMiss),GetColor(10,10,10),hdxfont(fPier_m20):swbreak			//MISS
						case 5:DrawStringToHandle 914+EasePosX,501,strf("%04d",0),GetColor(10,10,10),hdxfont(fPier_m20):swbreak				//EARLY
						case 6:DrawStringToHandle 914+EasePosX,537,strf("%04d",0),GetColor(10,10,10),hdxfont(fPier_m20):swbreak				//LATE
					swend
				loop

			//判定別グラフ
				dim JudgeGraphTmpPosY
				ddim JudgeGraphPerc,5:dim JudgeGraphHeight,5
				//各判定の割合を算出
					JudgeGraphPerc(0)=double(numExcellent)/double(hitobjMax)
					JudgeGraphPerc(1)=double(numGreat)/double(hitobjMax)
					JudgeGraphPerc(2)=double(numNice)/double(hitobjMax)
					JudgeGraphPerc(3)=double(numBad)/double(hitobjMax)
					JudgeGraphPerc(4)=double(numMiss)/double(hitobjMax)
				//各判定の割合からグラフの高さを算出
					JudgeGraphTmpPosY=291
					repeat 5
						JudgeGraphHeight(cnt)=int(200.0*double(JudgeGraphPerc(cnt)))
						DrawBox 1104,JudgeGraphTmpPosY,1104+20,JudgeGraphTmpPosY+JudgeGraphHeight(cnt),GetJudgeColor(5-cnt),TRUE
						JudgeGraphTmpPosY+JudgeGraphHeight(cnt)
					loop

			//判定別割合の表示
				repeat 5
					setease 0,256,ease_cubic_out
					SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(SceneCount-80-cnt*3,25)
					setease 32,0,ease_cubic_out
					EasePosX=getease(SceneCount-80-cnt*3,25)

					DrawStringToHandle 1014+EasePosX,305+36*cnt,strf("%3.1f",str(JudgeGraphPerc(cnt)*100.0))+"%",GetColor(10,10,10),hdxfont(fPier_r14)
				loop

			//ランクの表示
				setease 1.5,1,ease_cubic_out
					EaseWidth=int(300.0*geteasef(SceneCount-100,20))
					EaseHeight=int(128.0*geteasef(SceneCount-100,20))
					EasePosX=842+300/2-EaseWidth/2
					EasePosY=32+128/2-EaseHeight/2
				setease 0,256,ease_cubic_out
					SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount-100,20)

					DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdximg(iIcon_Rank_SSS+ScoreRankID),TRUE

	//ガイド
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		setease DispHeight,DispHeight-128,ease_cubic_out
		DrawGraph 0,getease(SceneCount-10,30),hdximg(ibg_GuidePanel),TRUE

		setease 0,256,ease_linear
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount-30,20)
		DrawGraph 540,650,hdximg(iGuide_Large),TRUE

		SetDrawBlendMode DX_BLENDMODE_ALPHA,256
		sdim tmpstr:tmpstr="ENTER : 次へ進む"
		DrawStringXCenterToHandle 540,658,strmid(tmpstr,0,limit(SceneCount-30,0,strlen(tmpstr))),200,GetColor(230,230,230),hdxfont(fNoto_m14)

	//前シーンからの引き継ぎ
	if SceneCount<50 {
		if SceneCount==1 {
			BASS_ChannelPlay hbasssnd(sndUI_Trans_Out)
		}
		setease 0,-DispWidth,ease_cubic_in
		EasePosX=getease(SceneCount,25)
		setease 0,DispHeight,ease_cubic_in
		EasePosY=getease(SceneCount,25)
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph EasePosX,EasePosY,hdximg(iTrans_In_L),TRUE

		setease 0,DispWidth,ease_cubic_in
		EasePosX=getease(SceneCount,25)
		setease 0,-DispHeight,ease_cubic_in
		EasePosY=getease(SceneCount,25)
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph EasePosX,EasePosY,hdximg(iTrans_In_R),TRUE

		setease 0,-DispWidth,ease_cubic_in
		EasePosX=getease(SceneCount,50)
		setease 0,DispHeight,ease_cubic_in
		EasePosY=getease(SceneCount,50)
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph EasePosX,EasePosY,hdximg(iTrans_Out_L),TRUE

		setease 0,DispWidth,ease_cubic_in
		EasePosX=getease(SceneCount,50)
		setease 0,-DispHeight,ease_cubic_in
		EasePosY=getease(SceneCount,50)
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph EasePosX,EasePosY,hdximg(iTrans_Out_R),TRUE
	}

	//トランジション
	if decideFlag==TRUE {
		//若干暗転
		setease 0,150,ease_linear
		SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(decideFrameCount,50)
		DrawBox 0,0,DispWidth,DispHeight,GetColor(0,0,0),TRUE

		//音量調整
		setease 0.4,0,ease_linear
		BASS_ChannelSetAttribute hbasssnd(bgmResult),BASS_ATTRIB_VOL,tofloat(geteasef(decideFrameCount,15))
		if decideFrameCount==0 {
			PlaySE hbasssnd(sndUI_Trans_In)
		}

		setease -DispWidth,0,ease_cubic_out
		EasePosX=getease(decideFrameCount,50)
		setease DispHeight,0,ease_cubic_out
		EasePosY=getease(decideFrameCount,50)
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph EasePosX,EasePosY,hdximg(iTrans_In_L),TRUE

		setease DispWidth,0,ease_cubic_out
		EasePosX=getease(decideFrameCount,50)
		setease -DispHeight,0,ease_cubic_out
		EasePosY=getease(decideFrameCount,50)
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph EasePosX,EasePosY,hdximg(iTrans_In_R),TRUE

		setease -DispWidth,0,ease_cubic_out
		EasePosX=getease(decideFrameCount,25)
		setease DispHeight,0,ease_cubic_out
		EasePosY=getease(decideFrameCount,25)
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph EasePosX,EasePosY,hdximg(iTrans_Out_L),TRUE

		setease DispWidth,0,ease_cubic_out
		EasePosX=getease(decideFrameCount,25)
		setease -DispHeight,0,ease_cubic_out
		EasePosY=getease(decideFrameCount,25)
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph EasePosX,EasePosY,hdximg(iTrans_Out_R),TRUE

		decideFrameCount++
	}

	if decideFrameCount==50+1 {
		//詳細記録読み込み
			gosub*LoadDetailRecord
		//シーン移行
			dim transEffFlag
			transEffFlag=TRUE
			gosub*Scene_Init_MusicSelect
	}
	if decideFrameCount>50+1 {
		//IR接続
		gosub*SyncIR
	}
	if decideFrameCount>50+1+60*3 {
		//シーン移行
		curSceneID=ID_MusicSelect
		gosub*InitScene
		//変数初期化
		dim decideFlag
		dim decideFrameCount
		dim transEffFlag
		transEffFlag=TRUE
	}
	return