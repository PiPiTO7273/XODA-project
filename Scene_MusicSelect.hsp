
*Scene_Init_MusicSelect

	//楽曲一覧を読み込み
	exist"Data/core_Playlist.dat"
	if strsize==-1 {
		AddDebugConsole STYLE_ERROR,"楽曲のデータベースが見つかりません。"
		goto*StopError
	} else {
		sdim SongLibBuf,strsize		//データベースのバッファ
		sdim NoteLine

		notesel SongLibBuf
		noteload"Data/core_Playlist.dat"

		sdim SongLibChartPath,64,3,notemax		//譜面ファイルの場所

		sdim Libarr_ChartID,32,3,notemax		//譜面ファイルのパス
		sdim Libarr_Title,64,3,notemax		//タイトル
		sdim Libarr_Artist,64,3,notemax		//アーティスト
		dim Libarr_Style,3,notemax			//プレイスタイル
		sdim Libarr_Difficulty,8,3,notemax		//難易度区分
		sdim Libarr_Lyrics,512,3,notemax		//歌詞データ
		dim Libarr_Level,3,notemax			//難易度数値
		dim Libarr_Flag,3,notemax			//使用可能フラグ
		ddim Libarr_DefBPM,3,notemax			//既定のBPM
		ddim Libarr_MinBPM,3,notemax			//最低BPM
		ddim Libarr_MaxBPM,3,notemax			//最高BPM
		dim Libarr_TotalNotes,3,notemax		//ノーツ数
		dim Libarr_Max						//項目数
		Libarr_Max=notemax

		dim SongLibLevID
		dim SongLibSelID

		//データベース解析
		repeat notemax
			notesel SongLibBuf
			noteget NoteLine,cnt
			split NoteLine,":",SongLibChartPath(0,cnt),SongLibChartPath(1,cnt),SongLibChartPath(2,cnt)
			SongLibSelID=cnt
			repeat 3
				Libarr_ChartID(cnt,SongLibSelID)=getpath(SongLibChartPath(cnt,SongLibSelID),8+1)
				exist"Data/"+SongLibChartPath(cnt,SongLibSelID)
				if strsize==-1 {
					//存在しない譜面ファイル
					if SongLibChartPath(cnt,SongLibSelID)!"-" {
						AddDebugConsole STYLE_WARNING,"存在しない譜面ファイルが指定されました。"
					}
					Libarr_Title(cnt,SongLibSelID)=""
					Libarr_Artist(cnt,SongLibSelID)=""
					Libarr_Style(cnt,SongLibSelID)=0
					Libarr_Difficulty(cnt,SongLibSelID)=""
					Libarr_Lyrics(cnt,SongLibSelID)=""
					Libarr_Level(cnt,SongLibSelID)=0
					Libarr_Flag(cnt,SongLibSelID)=FALSE
				} else {
					//構文解析
					sdim SongItemBuf,strsize
					sdim ChartCmdName
					sdim ChartPrm
					sdim ChartDivPrm
					notesel SongItemBuf
					noteload"Data/"+SongLibChartPath(cnt,SongLibSelID)

					SongLibLevID=cnt
					repeat notemax
						noteget NoteLine,cnt
						split NoteLine,":",chartCmdName,ChartPrm
						split ChartPrm,",",chartDivPrm

						switch chartCmdName
							case"#TITLE"
								Libarr_Title(SongLibLevID,SongLibSelID)=ChartPrm
							swbreak
							case"#ARTIST"
								Libarr_Artist(SongLibLevID,SongLibSelID)=ChartPrm
							swbreak
							case"#STYLE"
								if ChartPrm=="SHUTTLE" :Libarr_Style(SongLibLevID,SongLibSelID)=0
								if ChartPrm=="CLASSIC" :Libarr_Style(SongLibLevID,SongLibSelID)=1
							swbreak
							case"#DIFFICULTY"
								Libarr_Difficulty(SongLibLevID,SongLibSelID)=ChartPrm
							swbreak
							case"#LEVEL"
								Libarr_Level(SongLibLevID,SongLibSelID)=int(ChartPrm)
							swbreak
							case"#DEFBPM"
								Libarr_DefBPM(SongLibLevID,SongLibSelID)=double(ChartPrm)
							swbreak
							case"#SETBPM"
								if Libarr_MinBPM(SongLibLevID,SongLibSelID)==0.0&Libarr_MaxBPM(SongLibLevID,SongLibSelID)==0.0 {
									Libarr_DefBPM(SongLibLevID,SongLibSelID)=double(chartDivPrm(1))
								}
								if Libarr_MinBPM(SongLibLevID,SongLibSelID)==0.0 :Libarr_MinBPM(SongLibLevID,SongLibSelID)=double(ChartDivPrm(1))
								if Libarr_MaxBPM(SongLibLevID,SongLibSelID)==0.0 :Libarr_MaxBPM(SongLibLevID,SongLibSelID)=double(ChartDivPrm(1))
								if Libarr_MinBPM(SongLibLevID,SongLibSelID)>double(chartDivPrm(1)) {
									Libarr_MinBPM(SongLibLevID,SongLibSelID)=double(chartDivPrm(1))
								}
								if Libarr_MaxBPM(SongLibLevID,SongLibSelID)<double(chartDivPrm(1)) {
									Libarr_MaxBPM(SongLibLevID,SongLibSelID)=double(ChartDivPrm(1))
								}
							swbreak
							case"#LYRICS"
								strrep chartDivPrm(1),"*com",","	//置換
								Libarr_Lyrics(SongLibLevID,SongLibSelID)+chartDivPrm(1)+"\n"
							swbreak
							case"#OBJ"
								Libarr_TotalNotes(SongLibLevID,SongLibSelID)++
							swbreak
						swend
					loop
				}
			loop
		loop
	}

	//オンラインランキング初期化
		dim Libarr_IRrank,3,Libarr_Max,9
		sdim Libarr_IRname,16,3,Libarr_Max,9
		dim Libarr_IRscore,3,Libarr_Max,9
		dim Libarr_MyIRrank,3,Libarr_Max
		dim Libarr_MyIRscore,3,Libarr_Max
		sdim TargetIRdata
		sdim TargetIRchartID:sdim TargetIRrank
		sdim TargetIRname:sdim TargetIRscore
		sdim INIkey:sdim INIval
		dim IRsyncFlag:dim IRsyncFrame

	//変数初期化
		dim CursorSelID			//項目ID
		dim CursorLevID			//難易度ID
		dim CursorSelCount			//カーソル待機カウンタ
		dim KeyPushCount,2			//キー押下時間
		dim CursorOffsetEase		//カーソル移動のオフセット値
		dim DrawColumnID			//描画する項目ID
		dim DrawColumnPos			//描画する項目の位置
		dim LevColorCode			//難易度の色コード
		dim DetailWindowCount		//詳細ウィンドウの出現カウンタ
		dim DetailWindowPageID		//詳細ウィンドウのページID
		dim DetailWindowChangeFlag	//詳細ウィンドウのページ切り替えフラグ
		dim bfDetailWindowChangeFlag	//前フレームのページ切り替えフラグ
		dim JumpSceneFlag			//他シーンへのジャンプフラグ
		dim JumpSceneID			//他シーンのID
		dim JumpSceneCount			//他シーンへのジャンプを行うカウンタ

	//セーブデータから復元
		CursorSelID=aqMusicSelect_SelID
		CursorLevID=aqMusicSelect_LevID

	WebAPI_Post SERVICE_HTTP+"/API-GetGlobalRanking.php?key="+APIkey_GetGlobalRanking,sWebAPI
	gosub*Create_AQProfileBuf

	//詳細記録を解析
		dim Libarr_Rec_Score,3,Libarr_Max			//自己ベストスコア
		dim Libarr_Rec_NumExcellent,3,Libarr_Max	//Excellent判定数
		dim Libarr_Rec_NumGreat,3,Libarr_Max		//Great判定数
		dim Libarr_Rec_NumNice,3,Libarr_Max		//Nice判定数
		dim Libarr_Rec_NumBad,3,Libarr_Max			//Bad判定数
		dim Libarr_Rec_NumMiss,3,Libarr_Max		//Miss判定数
		dim Libarr_Rec_NumEarly,3,Libarr_Max		//Early判定数
		dim Libarr_Rec_NumLate,3,Libarr_Max		//Late判定数
		dim Libarr_Rec_MaxCombo,3,Libarr_Max		//最大コンボ数
		sdim Libarr_Rec_ClearType,16,3,Libarr_Max	//クリアの種類
		dim Libarr_Rec_Graph,3,Libarr_Max,270		//グラフキャプチャー値
		dim Libarr_Rec_ScoreRank,3,Libarr_Max		//スコアのランク

		sdim INIsection
		dim TargetArrLevID	//代入先レベルID
		dim TargetArrSelID	//代入先項目ID
		TargetArrLevID=-1
		TargetArrSelID=-1

		notesel DetailRecordBuf
		repeat notemax
			noteget NoteLine,cnt
			if strmid(NoteLine,0,1)=="["&strmid(NoteLine,-1,1)=="]" {
				//セクション行を検出
					INIsection=NoteLine
					strrep INIsection,"[",""
					strrep INIsection,"]",""

				//該当する項目のレベルとIDを特定
					TargetArrLevID=-1
					TargetArrSelID=-1
					dim CurArrID
					repeat Libarr_Max
						CurArrID=cnt
						repeat 3
							if INIsection==Libarr_ChartID(cnt,CurArrID) {
								TargetArrLevID=cnt
								TargetArrSelID=CurArrID
							}
						loop
					loop
			} else {

				if TargetArrLevID!-1&TargetArrSelID!-1 {
					split NoteLine,"=",INIkey,INIval
					switch INIkey
						case"Score"
							Libarr_Rec_Score(TargetArrLevID,TargetArrSelID)=int(INIval)
							Libarr_Rec_ScoreRank(TargetArrLevID,TargetArrSelID)=RANK_ID_D
							if Libarr_Rec_Score(TargetArrLevID,TargetArrSelID)>=RANK_SCORE_C :Libarr_Rec_ScoreRank(TargetArrLevID,TargetArrSelID)=RANK_ID_C
							if Libarr_Rec_Score(TargetArrLevID,TargetArrSelID)>=RANK_SCORE_B :Libarr_Rec_ScoreRank(TargetArrLevID,TargetArrSelID)=RANK_ID_B
							if Libarr_Rec_Score(TargetArrLevID,TargetArrSelID)>=RANK_SCORE_A :Libarr_Rec_ScoreRank(TargetArrLevID,TargetArrSelID)=RANK_ID_A
							if Libarr_Rec_Score(TargetArrLevID,TargetArrSelID)>=RANK_SCORE_AA :Libarr_Rec_ScoreRank(TargetArrLevID,TargetArrSelID)=RANK_ID_AA
							if Libarr_Rec_Score(TargetArrLevID,TargetArrSelID)>=RANK_SCORE_AAA :Libarr_Rec_ScoreRank(TargetArrLevID,TargetArrSelID)=RANK_ID_AAA
							if Libarr_Rec_Score(TargetArrLevID,TargetArrSelID)>=RANK_SCORE_S :Libarr_Rec_ScoreRank(TargetArrLevID,TargetArrSelID)=RANK_ID_S
							if Libarr_Rec_Score(TargetArrLevID,TargetArrSelID)>=RANK_SCORE_SS :Libarr_Rec_ScoreRank(TargetArrLevID,TargetArrSelID)=RANK_ID_SS
							if Libarr_Rec_Score(TargetArrLevID,TargetArrSelID)>=RANK_SCORE_SSS :Libarr_Rec_ScoreRank(TargetArrLevID,TargetArrSelID)=RANK_ID_SSS
						swbreak
						case"NumExcellent"
							Libarr_Rec_NumExcellent(TargetArrLevID,TargetArrSelID)=int(INIval)
						swbreak
						case"NumGreat"
							Libarr_Rec_NumGreat(TargetArrLevID,TargetArrSelID)=int(INIval)
						swbreak
						case"NumNice"
							Libarr_Rec_NumNice(TargetArrLevID,TargetArrSelID)=int(INIval)
						swbreak
						case"NumBad"
							Libarr_Rec_NumBad(TargetArrLevID,TargetArrSelID)=int(INIval)
						swbreak
						case"NumMiss"
							Libarr_Rec_NumMiss(TargetArrLevID,TargetArrSelID)=int(INIval)
						swbreak
						case"NumEarly"
							Libarr_Rec_NumEarly(TargetArrLevID,TargetArrSelID)=int(INIval)
						swbreak
						case"NumLate"
							Libarr_Rec_NumLate(TargetArrLevID,TargetArrSelID)=int(INIval)
						swbreak
						case"MaxCombo"
							Libarr_Rec_MaxCombo(TargetArrLevID,TargetArrSelID)=int(INIval)
						swbreak
						case"Type"
							Libarr_Rec_ClearType(TargetArrLevID,TargetArrSelID)=INIval
						swbreak
						case"Graph"
							sdim GraphBin,270
							GraphBin=INIval
							b64decode GraphBin,GraphBin
							repeat 270
								Libarr_Rec_Graph(TargetArrLevID,TargetArrSelID,cnt)=int(double(peek(GraphBin,cnt))*1.1)
							loop
						swbreak
					swend
				}

			}
		loop

	//プレビューBGM読み込み
	repeat length(hBGMpreview)
		if hBGMpreview(cnt)!0 {
			BASS_StreamFree hBGMpreview
		}
	loop
	dim hBGMFX,3*Libarr_Max
	repeat Libarr_Max
		TargetArrSelID=cnt
		repeat 3
			TargetArrLevID=cnt
			hBGMpreview(3*TargetArrSelID+TargetArrLevID)=BASS_StreamCreateFile(FALSE,"Data/Package/"+Libarr_ChartID(TargetArrLevID,TargetArrSelID)+"/preview.wav")
			hBGMFX(3*TargetArrSelID+TargetArrLevID)=SetAudioFXtohandle(hBGMpreview(3*TargetArrSelID+TargetArrLevID),aqFxid)
		loop
	loop

	if transEffFlag==TRUE {
		BASS_ChannelSetAttribute hbasssnd(bgmMain),BASS_ATTRIB_VOL,tofloat(0.7)
		BASS_ChannelSetPosition hbasssnd(bgmMain),0,0,0
		BASS_ChannelPlay hbasssnd(bgmMain)
	}

	return

*Scene_MusicSelect

	//IR接続
	if transEffFlag==FALSE :gosub*SyncIR

	//キー判定
		//左右（難易度変更）
			if bfKeyFlag(KEY_INPUT_LEFT)==0&KeyFlag(KEY_INPUT_LEFT)==1 {		//←
				CursorOffsetEase=0
				CursorSelCount=0
				CursorLevID--
				PlaySE hbasssnd(sndUI_ChangeDifficulty)
			}
			if bfKeyFlag(KEY_INPUT_RIGHT)==0&KeyFlag(KEY_INPUT_RIGHT)==1 {		//→
				CursorOffsetEase=0
				CursorSelCount=0
				CursorLevID++
				PlaySE hbasssnd(sndUI_ChangeDifficulty)
			}
			if CursorLevID<0 :CursorLevID=2
			if CursorLevID>2 :CursorLevID=0

		//上下（カーソル移動）
			if bfKeyFlag(KEY_INPUT_UP)==1&KeyFlag(KEY_INPUT_UP)==1 {		//↑
				KeyPushCount(0)++
			} else :KeyPushCount(0)=0
			if (bfKeyFlag(KEY_INPUT_UP)==0&KeyFlag(KEY_INPUT_UP)==1)|(KeyPushCount(0)>16&SceneCount\2==0) {	//連続↑
				CursorSelCount=0
				CursorSelID--
				if CursorSelID<0 :CursorSelID=Libarr_Max-1
				CursorOffsetEase=-40
				PlaySE hbasssnd(sndUI_Cursor)
			}
			if bfKeyFlag(KEY_INPUT_DOWN)==1&KeyFlag(KEY_INPUT_DOWN)==1 {		//↓
				KeyPushCount(1)++
			} else :KeyPushCount(1)=0
			if (bfKeyFlag(KEY_INPUT_DOWN)==0&KeyFlag(KEY_INPUT_DOWN)==1)|(KeyPushCount(1)>16&SceneCount\2==0) {	//連続↓
				CursorSelCount=0
				CursorSelID++
				if CursorSelID>=Libarr_Max :CursorSelID=0
				CursorOffsetEase=40
				PlaySE hbasssnd(sndUI_Cursor)
			}

		//決定キー判定
			if KeyFlag(KEY_INPUT_RETURN)==TRUE {
				exist"Data/Package/"+Libarr_ChartID(CursorLevID,CursorSelID)+"/audio.mp3"
				if strsize==-1 {
					dim DialogCount:dim DialogFlag
					PlaySE hbasssnd(sndUI_Error)
					gosub*Scene_MusicSelect_OpenError
				} else {
					repeat 3*Libarr_Max
						BASS_ChannelStop hBGMpreview(cnt)
					loop
					BASS_ChannelSetAttribute hbasssnd(bgmMain),BASS_ATTRIB_VOL,tofloat(0.7)
					decideFlag=TRUE
				}
			}

		//ページ切り替えキー判定
			DetailWindowChangeFlag=KeyFlag(KEY_INPUT_4)
			if bfDetailWindowChangeFlag==0&DetailWindowChangeFlag==1 {
				DetailWindowPageID++
				if DetailWindowPageID>3 :DetailWindowPageID=0
				if DetailWindowPageID==3 {
					//未実装アイテム
					CreateNotificationBuf hdximg(iLayer_Notice),ID_WARNING,"現在、未実装の機能です。",""
					SendNotification hdximg(iLayer_Notice)
					SetDrawScreen DX_SCREEN_BACK
				}
			}
			bfDetailWindowChangeFlag=DetailWindowChangeFlag

		//オプション
			if KeyFlag(KEY_INPUT_1)==TRUE&JumpSceneFlag==FALSE {
				JumpSceneCount=0
				JumpSceneFlag=TRUE
				JumpSceneID=ID_MusicSelect_Option
				PlaySE hbasssnd(sndUI_Decide)
			}
		//エフェクト
			if KeyFlag(KEY_INPUT_2)==TRUE&JumpSceneFlag==FALSE {
				JumpSceneCount=0
				JumpSceneFlag=TRUE
				JumpSceneID=ID_MusicSelect_Effect
				PlaySE hbasssnd(sndUI_Decide)
			}
		//パートナー
			if KeyFlag(KEY_INPUT_3)==TRUE&JumpSceneFlag==FALSE {
				JumpSceneCount=0
				JumpSceneFlag=TRUE
				JumpSceneID=ID_MusicSelect_Partner
				PlaySE hbasssnd(sndUI_Decide)
			}

	//BGM関係
		if CursorSelCount==15 {
			BASS_ChannelSetAttribute hbasssnd(bgmMain),BASS_ATTRIB_VOL,tofloat(0.0)
			BASS_ChannelSetAttribute hBGMpreview(3*CursorSelID+CursorLevID),BASS_ATTRIB_VOL,tofloat(0.7)
			BASS_ChannelSetPosition hBGMpreview(3*CursorSelID+CursorLevID),0,0,0
			BASS_ChannelPlay hBGMpreview(3*CursorSelID+CursorLevID)
		}
		if CursorSelCount<15 {
			repeat 3*Libarr_Max
				BASS_ChannelStop hBGMpreview(cnt)
			loop
			BASS_ChannelSetAttribute hbasssnd(bgmMain),BASS_ATTRIB_VOL,tofloat(0.7)
		}
		if CursorSelCount>15+60*9 {
			setease 0.7,0.0,ease_linear
			BASS_ChannelSetAttribute hBGMpreview(3*CursorSelID+CursorLevID),BASS_ATTRIB_VOL,tofloat(geteasef(CursorSelCount-15-60*9,20))
		}
		if CursorSelCount>15+60*9+20 {
			setease 0.0,0.7,ease_linear
			BASS_ChannelSetAttribute hbasssnd(bgmMain),BASS_ATTRIB_VOL,tofloat(geteasef(CursorSelCount-15-60*9-20,20))
		}

		//プレビュー中のビジュアライザー
			SetDrawScreen hdximg(iPlay_Visualizer)
				ClearDrawScreen
				SetDrawBlendMode DX_BLENDMODE_ALPHA,256
				dim bgmFFTbuf,4096:dim bgmFFTline
				BASS_ChannelGetData hBGMpreview(3*CursorSelID+CursorLevID),varptr(bgmFFTbuf),BASS_DATA_FFT8192

				setease 0,191,ease_linear+ease_loop
				hsvcolor getease(SceneCount,200),100,240

				repeat 32
					bgmFFTline=int(todouble(bgmFFTbuf(128/32*cnt))*2500)
					if bgmFFTline<100 :bgmFFTline*4
					DrawBox cnt*(1024/32),512-bgmFFTline,(cnt+1)*(1024/32)-2,512,GetColor(ginfo_r,ginfo_g,ginfo_b),TRUE
				loop
			SetDrawScreen DX_SCREEN_BACK

	//背景
		SetDrawBlendMode DX_BLENDMODE_NOBLEND,256
		DrawGraph 0,0,hdximg(iBackground),FALSE

		gosub*DrawMusicSelectNav

	//----- 以下、シーン別レイヤー -----
		SetDrawScreen hdximg(iLayer_Scene)
		ClearDrawScreen

		//項目
			//基準より下
				DrawColumnID=CursorSelID
				DrawColumnPos=314
				repeat
					setease CursorOffsetEase,0,ease_cubic_out
					EasePosY=DrawColumnPos+getease(CursorSelCount,12)
					if cnt==0 {
						setease 0,-32,ease_cubic_out
						EasePosX=getease(CursorSelCount,12)
					} else :EasePosX=0

					gosub*DrawColumnGraph

					DrawColumnPos+40
					if EasePosY>DispHeight :break
					DrawColumnID++
					if DrawColumnID>=Libarr_Max :DrawColumnID=0
				loop
			//基準より上
				DrawColumnID=CursorSelID
				DrawColumnPos=314
				EasePosX=0
				repeat
					setease CursorOffsetEase,0,ease_cubic_out
					EasePosY=DrawColumnPos+getease(CursorSelCount,12)
					if cnt!0 :gosub*DrawColumnGraph
					DrawColumnPos-40
					if EasePosY<-40 :break
					DrawColumnID--
					if DrawColumnID<0 :DrawColumnID=Libarr_Max-1
				loop

		//楽曲情報

			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph 0,120,hdximg(ibg_MusicSelect_ItemInfo),TRUE

			setease 0,256,ease_cubic_out
			EaseAlpha=getease(CursorSelCount,20)
			setease -32,0,ease_cubic_out
			EasePosX=getease(CursorSelCount,20)

			SetDrawBlendMode DX_BLENDMODE_ALPHA,EaseAlpha
			DrawStringToHandle 50+EasePosX,146,Libarr_Title(CursorLevID,CursorSelID),GetColor(250,250,250),hdxfont(fNoto_m24)	//タイトル
			DrawStringToHandle 50+EasePosX,191,Libarr_Artist(CursorLevID,CursorSelID),GetColor(230,230,230),hdxfont(fNoto_m14)	//アーティスト名
			sdim tmpstr:tmpstr=Libarr_Difficulty(CursorLevID,CursorSelID)+" Lv."+Libarr_Level(CursorLevID,CursorSelID)
			DrawStringToHandle 50+EasePosX,227,tmpstr,GetDifficultyColor(Libarr_Difficulty(CursorLevID,CursorSelID)),hdxfont(fGood_14)	//難易度

			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,int(double(EaseAlpha)*0.2)
			switch Libarr_Style(CursorLevID,CursorSelID)
				case 0:DrawGraph 360+EasePosX,137,hdximg(iIcon_ShuttleStyle),TRUE:swbreak
				case 1:DrawGraph 360+EasePosX,137,hdximg(iIcon_ClassicStyle),TRUE:swbreak
			swend

		//プレイスタイル
			setease 0,256,ease_cubic_out
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(CursorSelCount,20)
			setease 1.5,1.0,ease_cubic_out
			EaseWidth=int(237.0*geteasef(CursorSelCount,20))
			EaseHeight=int(300.0*geteasef(CursorSelCount,20))
			EasePosX=521+237/2-EaseWidth/2
			EasePosY=-50+300/2-EaseHeight/2
			switch Libarr_Style(CursorLevID,CursorSelID)
				case 0:DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdximg(iLayer_ShuttleStyle),TRUE:swbreak
				case 1:DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdximg(iLayer_ClassicStyle),TRUE:swbreak
			swend

		//詳細
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph 0,278,hdximg(ibg_MusicSelect_Detail),TRUE

			switch DetailWindowPageID
				//オンラインランキング
					case ID_IR
						SetDrawBlendMode DX_BLENDMODE_ALPHA,256
						DrawStringToHandle 20,278+12,"ONLINE",GetColor(240,240,240),hdxfont(fPier_m16)
						if DetailWindowCount==0 {
							//バッファ生成
								SetDrawScreen hdximg(iLayer_Detail_IR)
								ClearDrawScreen

								SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
								DrawGraph 24,46+28*0,hdximg(iIcon_IR_1st),TRUE		//1st アイコン
								DrawGraph 24,46+28*1,hdximg(iIcon_IR_2nd),TRUE		//2nd アイコン
								DrawGraph 24,46+28*2,hdximg(iIcon_IR_3rd),TRUE		//3rd アイコン
								//1-3
								SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
								repeat 3
									if Libarr_IRscore(CursorLevID,CursorSelID,cnt)!0 {
										//値あり
										DrawStringToHandle 52,47+28*cnt,"#"+(Libarr_IRrank(CursorLevID,CursorSelID,cnt)+1),GetColor(240,240,240),hdxfont(fSQ_12)
										DrawStringToHandle 84,47+28*cnt,Libarr_IRname(CursorLevID,CursorSelID,cnt),GetColor(240,240,240),hdxfont(fPier_m12)
										DrawStringToHandle 204,42+28*cnt,strf("%08d",Libarr_IRscore(CursorLevID,CursorSelID,cnt)),GetColor(240,240,240),hdxfont(fPier_m20)
									} else {
										//値なし
										DrawStringToHandle 84,47+28*cnt,"-",GetColor(240,240,240),hdxfont(fPier_m12)
										DrawStringToHandle 52,47+28*cnt,"#-",GetColor(240,240,240),hdxfont(fSQ_12)
										DrawStringToHandle 204,42+28*cnt,"--------",GetColor(240,240,240),hdxfont(fPier_m20)
									}
								loop

								//4-
								dim numIRlist
								if Libarr_MyIRrank(CursorLevID,CursorSelID)==0&Libarr_MyIRscore(CursorLevID,CursorSelID)==0 {
									numIRlist=6
								} else {
									numIRlist=4

									//自己ベストデータ
										SetDrawBlendMode DX_BLENDMODE_ALPHA,256
										DrawStringToHandle 24,238,"あなたのスコア",GetColor(240,240,240),hdxfont(fNoto_b12)
										SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
										DrawGraph 36,271,hdximg(iIcon_IR_best),TRUE
										DrawStringToHandle 71,263,"#"+(Libarr_MyIRrank(CursorLevID,CursorSelID)+1),GetColor(240,240,240),hdxfont(fSQ_12)
										DrawStringToHandle 71,283,aqName,GetColor(240,240,240),hdxfont(fPier_m16)
										DrawStringToHandle 236,270,strf("%08d",Libarr_MyIRscore(CursorLevID,CursorSelID)),GetColor(240,240,240),hdxfont(fPier_m22)
								}
								repeat numIRlist
									SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
									DrawGraph 24,46+28*(3+cnt),hdximg(iIcon_IR),TRUE

									SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
									if Libarr_IRscore(CursorLevID,CursorSelID,3+cnt)!0 {
										//値あり
										DrawStringToHandle 84,47+28*(3+cnt),Libarr_IRname(CursorLevID,CursorSelID,3+cnt),GetColor(240,240,240),hdxfont(fPier_m12)
										DrawStringToHandle 52,47+28*(3+cnt),"#"+(Libarr_IRrank(CursorLevID,CursorSelID,3+cnt)+1),GetColor(240,240,240),hdxfont(fSQ_12)
										DrawStringToHandle 204,42+28*(3+cnt),strf("%08d",Libarr_IRscore(CursorLevID,CursorSelID,3+cnt)),GetColor(240,240,240),hdxfont(fPier_m20)
									} else {
										//値なし
										DrawStringToHandle 84,47+28*(3+cnt),"-",GetColor(240,240,240),hdxfont(fPier_m12)
										DrawStringToHandle 52,47+28*(3+cnt),"#-",GetColor(240,240,240),hdxfont(fSQ_12)
										DrawStringToHandle 204,42+28*(3+cnt),"--------",GetColor(240,240,240),hdxfont(fPier_m20)
									}
								loop

								SetDrawScreen hdximg(iLayer_Scene)
						}
						SetDrawBlendMode DX_BLENDMODE_ALPHA,256
						DrawGraph 0,278,hdximg(iLayer_Detail_IR),TRUE
					swbreak

				//自己ベスト詳細
					case ID_RECORD
						SetDrawBlendMode DX_BLENDMODE_ALPHA,256
						DrawStringToHandle 20,278+12,"MY RECORD",GetColor(240,240,240),hdxfont(fPier_m16)

						if DetailWindowCount==0 {
							//バッファ生成
								if hdximg(iLayer_Detail_Record)!0 :DeleteGraph hdximg(iLayer_Detail_Record)
								hdximg(iLayer_Detail_Record)=MakeScreen(400,330,TRUE)
								SetDrawScreen hdximg(iLayer_Detail_Record)
								SetDrawBlendMode DX_BLENDMODE_ALPHA,256

							//タイトル
								DrawStringToHandle 24,42,"自己ベスト",GetColor(240,240,240),hdxfont(fNoto_m12)
								DrawStringToHandle 128,43,strf("%08d",Libarr_Rec_Score(CursorLevID,CursorSelID)),GetColor(240,240,240),hdxfont(fPier_r14)
								DrawStringToHandle 24,188,"ゲージ推移",GetColor(240,240,240),hdxfont(fNoto_m12)

							//判定別
								DrawStringToHandle 42,68,"Excellent",GetJudgeColor(JUDGE_RES_EXCELLENT),hdxfont(fPier_r14)
								DrawStringToHandle 42,89,"Great",GetJudgeColor(JUDGE_RES_GREAT),hdxfont(fPier_r14)
								DrawStringToHandle 42,110,"Nice",GetJudgeColor(JUDGE_RES_NICE),hdxfont(fPier_r14)
								DrawStringToHandle 42,131,"Bad",GetJudgeColor(JUDGE_RES_BAD),hdxfont(fPier_r14)
								DrawStringToHandle 42,152,"Miss",GetJudgeColor(JUDGE_RES_MISS),hdxfont(fPier_r14)
								DrawStringToHandle 210,68,"Early",GetColor(255,68,218),hdxfont(fPier_r14)
								DrawStringToHandle 210,89,"Late",GetColor(74,206,255),hdxfont(fPier_r14)
								DrawStringToHandle 210,110,"Max Combo",GetColor(230,230,230),hdxfont(fPier_r14)

							//値
								DrawStringToHandle 140,68,strf("%04d",Libarr_Rec_NumExcellent(CursorLevID,CursorSelID)),GetColor(230,230,230),hdxfont(fPier_r14)
								DrawStringToHandle 140,89,strf("%04d",Libarr_Rec_NumGreat(CursorLevID,CursorSelID)),GetColor(230,230,230),hdxfont(fPier_r14)
								DrawStringToHandle 140,110,strf("%04d",Libarr_Rec_NumNice(CursorLevID,CursorSelID)),GetColor(230,230,230),hdxfont(fPier_r14)
								DrawStringToHandle 140,131,strf("%04d",Libarr_Rec_NumBad(CursorLevID,CursorSelID)),GetColor(230,230,230),hdxfont(fPier_r14)
								DrawStringToHandle 140,152,strf("%04d",Libarr_Rec_NumMiss(CursorLevID,CursorSelID)),GetColor(230,230,230),hdxfont(fPier_r14)
								DrawStringToHandle 308,68,strf("%04d",Libarr_Rec_NumEarly(CursorLevID,CursorSelID)),GetColor(230,230,230),hdxfont(fPier_r14)
								DrawStringToHandle 308,89,strf("%04d",Libarr_Rec_NumLate(CursorLevID,CursorSelID)),GetColor(230,230,230),hdxfont(fPier_r14)
								DrawStringToHandle 308,110,strf("%04d",Libarr_Rec_MaxCombo(CursorLevID,CursorSelID)),GetColor(230,230,230),hdxfont(fPier_r14)

							//グラフ
								SetDrawBlendMode DX_BLENDMODE_ALPHA,50
								DrawBox 108,188,108+270,188+110,GetColor(255,255,255),FALSE
								SetDrawBlendMode DX_BLENDMODE_ALPHA,256
								repeat 270-1
									DrawLine 108+cnt,188+110-Libarr_Rec_Graph(CursorLevID,CursorSelID,cnt),108+cnt+1,188+110-Libarr_Rec_Graph(CursorLevID,CursorSelID,cnt+1),GetColor(255,255,255),TRUE
								loop

							SetDrawScreen hdximg(iLayer_Scene)
						}
						SetDrawBlendMode DX_BLENDMODE_ALPHA,256
						DrawGraph 0,278,hdximg(iLayer_Detail_Record),TRUE
					swbreak

				//歌詞
					case ID_LYRICS
						SetDrawBlendMode DX_BLENDMODE_ALPHA,256
						DrawStringToHandle 20,278+12,"LYRICS",GetColor(240,240,240),hdxfont(fPier_m16)
						DrawStringToHandle 20,278+41,Libarr_Lyrics(CursorLevID,CursorSelID),GetColor(200,200,200),hdxfont(fNoto_m12)
					swbreak

				//ターミナル
					case ID_TERMINAL
						SetDrawBlendMode DX_BLENDMODE_ALPHA,256
						DrawStringToHandle 20,278+12,"TERMINAL",GetColor(240,240,240),hdxfont(fPier_m16)
					swbreak
			swend

		//難易度選択
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph 420,300,hdximg(ibg_MusicSelect_Level),TRUE
			DrawGraph 420,300,hdximg(iLayer_LevelSelect),TRUE

			//ディスクアイコン
				DrawRotaGraph 434+96*CursorLevID+80/2,330+80/2,1.0,deg2rad(SceneCount*8\360),hdximg(ibg_Disc_Normal+CursorLevID),TRUE,FALSE
				repeat 3
					if cnt==CursorLevID :continue
					DrawGraph 434+96*cnt,330,hdximg(ibg_Disc),TRUE
				loop

			//難易度数値
				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
				DrawGraph 434+96*0,330,hdximg(iLayer_LevelSelect_Normal),TRUE
				DrawGraph 434+96*1,330,hdximg(iLayer_LevelSelect_Hard),TRUE
				DrawGraph 434+96*2,330,hdximg(iLayer_LevelSelect_Chaos),TRUE

				SetDrawBlendMode DX_BLENDMODE_ALPHA,256
				repeat 3
					setease 0,Libarr_Level(cnt,CursorSelID),ease_cubic_out
					tmpstr=str(getease(CursorSelCount,25))
					DrawStringXCenterToHandle 434+96*cnt,387,tmpstr,80,GetColor(255,255,255),hdxfont(fGood_12)
				loop

			//プレイヤーデータ,譜面情報
				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
				DrawStringToHandle 470,449,aqName,GetColor(20,20,20),hdxfont(fPier_m12)
				DrawStringToHandle 470,467,strf("%.2f",str(aqRate)),GetColor(20,20,20),hdxfont(fPier_m12)
				setease 0,Libarr_TotalNotes(CursorLevID,CursorSelID),ease_cubic_out
				sdim tmpstr:tmpstr=strf("%04d",str(getease(CursorSelCount,20)))
				DrawStringToHandle 650,449,tmpstr,GetColor(20,20,20),hdxfont(fPier_m12)
				setease 0,Libarr_DefBPM(CursorLevID,CursorSelID),ease_cubic_out
				sdim tmpstr:tmpstr=strf("%04d",str(getease(CursorSelCount,20)))
				DrawStringToHandle 615,467,tmpstr,GetColor(20,20,20),hdxfont(fPier_m12)
				if Libarr_DefBPM(CursorLevID,CursorSelID)!Libarr_MinBPM(CursorLevID,CursorSelID)&Libarr_DefBPM(CursorLevID,CursorSelID)!Libarr_MaxBPM(CursorLevID,CursorSelID) {
					DrawStringToHandle 652,470,strf("%04d - %04d",str(Libarr_MinBPM(CursorLevID,CursorSelID)),str(Libarr_MaxBPM(CursorLevID,CursorSelID))),GetColor(50,50,50),hdxfont(fPier_m8)
				}
				foreach aqHistory
					DrawStringToHandle 440,505+15*cnt,aqHistory(cnt),GetColor(20,20,20),hdxfont(fNoto_r12)
				loop

		//パートナー選択
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph 860,0,hdximg(ibg_MusicSelect_Partner),TRUE

			//パートナーアイコン
				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
				DrawGraph 875,28,pnhIcon(pnCurID),TRUE

			//パートナー情報
				SetDrawBlendMode DX_BLENDMODE_ALPHA,256
				DrawStringToHandle 970,32,pnName(pnCurID),GetColor(240,240,240),hdxfont(fNoto_m16)
				DrawStringToHandle 970,64,"Lv. "+pnLevel(pnCurID),GetColor(220,220,220),hdxfont(fPier_m12)
				DrawStringToHandle 1020,58,pnDesc(pnCurID),GetColor(201,253,255),hdxfont(fNoto_m12)

		//シーン別レイヤー
			SetDrawScreen DX_SCREEN_BACK
			if JumpSceneFlag==FALSE {
				setease 1.2,1,ease_cubic_out
				EaseWidth=int(double(DispWidth)*geteasef(SceneCount,20))
				EaseHeight=int(double(DispHeight)*geteasef(SceneCount,20))
				EasePosX=DispWidth/2-EaseWidth/2
				EasePosY=DispHeight/2-EaseHeight/2
				setease 0,256,ease_cubic_out
				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount,20)
				DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdximg(iLayer_Scene),TRUE
			} else {
				setease 1,1.2,ease_cubic_out
				EaseWidth=int(double(DispWidth)*geteasef(JumpSceneCount,20))
				EaseHeight=int(double(DispHeight)*geteasef(JumpSceneCount,20))
				EasePosX=DispWidth/2-EaseWidth/2
				EasePosY=DispHeight/2-EaseHeight/2
				setease 256,0,ease_cubic_out
				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(JumpSceneCount,20)
				DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdximg(iLayer_Scene),TRUE

				//プリローダー
				setease 0,100,ease_cubic_out
				EaseWidth=getease(JumpSceneCount,20)
				EaseHeight=getease(JumpSceneCount,20)
				EasePosX=DispWidth/2-EaseWidth/2
				EasePosY=DispHeight/2-EaseHeight/2
				setease 0,256,ease_cubic_out
				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(JumpSceneCount,20)
				DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdxarr_Blocks(FrameCount/2\20),TRUE
			}

		//選曲カーソルの移動アニメーション
			if CursorSelCount<35 {
				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
				DrawGraph 0,109,hdxarr_MusicSelect_Action(CursorSelCount),TRUE
			}

	//プリローダ
		setease 100,0,ease_cubic_out
		EaseWidth=getease(SceneCount-10-IRsyncFrame,20)
		EaseHeight=getease(SceneCount-10-IRsyncFrame,20)
		EasePosX=DispWidth/2-EaseWidth/2
		EasePosY=DispHeight/2-EaseHeight/2
		setease 256,0,ease_cubic_out
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount-10-IRsyncFrame,20)
		DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdxarr_Blocks(FrameCount/2\20),TRUE

	//ガイド
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph 0,DispHeight-128,hdximg(ibg_GuidePanel),TRUE

		//ビジュアライザー
			if CursorSelCount>15 {
				setease 128,0,ease_linear
				SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(CursorSelCount-15-60*9,25)
				DrawModiGraph 350,500,DispWidth-350,500,DispWidth-180,DispHeight,180,DispHeight,hdximg(iPlay_Visualizer),TRUE
			}

		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph 540,650,hdximg(iGuide_Large),TRUE
		DrawGraph 370,640,hdximg(iGuide_Small),TRUE
		DrawGraph 760,640,hdximg(iGuide_Small),TRUE
		DrawGraph 370,676,hdximg(iGuide_Small),TRUE
		DrawGraph 760,676,hdximg(iGuide_Small),TRUE

		SetDrawBlendMode DX_BLENDMODE_ALPHA,256
		sdim tmpstr:tmpstr="ENTER : 決定"
		DrawStringXCenterToHandle 540,658,strmid(tmpstr,0,limit(SceneCount,0,strlen(tmpstr))),200,GetColor(230,230,230),hdxfont(fNoto_m14)
		sdim tmpstr:tmpstr="1 : オプション"
		DrawStringXCenterToHandle 370,648,strmid(tmpstr,0,limit(SceneCount,0,strlen(tmpstr))),150,GetColor(230,230,230),hdxfont(fNoto_m14)
		sdim tmpstr:tmpstr="2 : エフェクト設定"
		DrawStringXCenterToHandle 760,648,strmid(tmpstr,0,limit(SceneCount,0,strlen(tmpstr))),150,GetColor(230,230,230),hdxfont(fNoto_m14)
		sdim tmpstr:tmpstr="↑↓ : カーソル移動"
		DrawStringXCenterToHandle 370,684,strmid(tmpstr,0,limit(SceneCount,0,strlen(tmpstr))),150,GetColor(230,230,230),hdxfont(fNoto_m14)
		sdim tmpstr:tmpstr="←→ : 難易度変更"
		DrawStringXCenterToHandle 760,684,strmid(tmpstr,0,limit(SceneCount,0,strlen(tmpstr))),150,GetColor(230,230,230),hdxfont(fNoto_m14)

	//リザルトからのトランジション
		if SceneCount<50&transEffFlag==TRUE {
			//若干暗転
				setease 0,150,ease_linear
				SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(decideFrameCount,50)
				DrawBox 0,0,DispWidth,DispHeight,GetColor(0,0,0),TRUE

			if SceneCount==1 {
				BASS_ChannelPlay hbasssnd(sndUI_Trans_Out)
			}
			setease 0,-DispWidth,ease_cubic_in
			EasePosX=getease(SceneCount,25)
			setease 0,DispHeight,ease_cubic_in
			EasePosY=getease(SceneCount,25)
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph EasePosX,EasePosY,hdximg(iTrans_In_L),TRUE

			setease 0,DispWidth,ease_cubic_in
			EasePosX=getease(SceneCount,25)
			setease 0,-DispHeight,ease_cubic_in
			EasePosY=getease(SceneCount,25)
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph EasePosX,EasePosY,hdximg(iTrans_In_R),TRUE

			setease 0,-DispWidth,ease_cubic_in
			EasePosX=getease(SceneCount,50)
			setease 0,DispHeight,ease_cubic_in
			EasePosY=getease(SceneCount,50)
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph EasePosX,EasePosY,hdximg(iTrans_Out_L),TRUE

			setease 0,DispWidth,ease_cubic_in
			EasePosX=getease(SceneCount,50)
			setease 0,-DispHeight,ease_cubic_in
			EasePosY=getease(SceneCount,50)
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph EasePosX,EasePosY,hdximg(iTrans_Out_R),TRUE
		}

	//カウンタ
		CursorSelCount++
		if decideFlag==TRUE :decideFrameCount++
		if decideFrameCount>0 {
			//決定画面
			SeekMovieToGraph hdximg(iMovie_Decide),0
			PlayMovieToGraph hdximg(iMovie_Decide)
			PlaySE hbasssnd(sndUI_Decide)
			repeat 60*DecideWaitTime
				ProcessMessage
				SetDrawScreen DX_SCREEN_BACK
				ClearDrawScreen
					SetDrawBlendMode DX_BLENDMODE_NOBLEND,256
					DrawGraph 0,0,hdximg(iMovie_Decide),FALSE

					//プレイする楽曲情報
						if cnt<25 {
							setease 0,256,ease_cubic_out
							SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(cnt,25)
						} else {
							setease 0,256,ease_cubic_in
							SetDrawBlendMode DX_BLENDMODE_ALPHA,256-getease(cnt-60*5+25,25)
						}
						dim tmpease
						if cnt<25 {
							setease 0,128,ease_cubic_out
							tmpease=getease(cnt,25)
						} else {
							setease 0,128,ease_cubic_in
							tmpease=128-getease(cnt-60*5+25,25)
						}

						//ステージ名
							sdim tmpstr:tmpstr="NORMAL MODE"
							switch Libarr_Style(CursorLevID,CursorSelID)
								case 0:tmpstr="- Shuttle Style -":swbreak
								case 1:tmpstr="- Classic Style -":swbreak
							swend
							DrawStringXCenterToHandle -128+tmpease,280,tmpstr,DispWidth,GetColor(50,50,50),hdxfont(fPier_m16)
						//曲名
							DrawStringXCenterToHandle 128-tmpease,315,Libarr_Title(CursorLevID,CursorSelID),DispWidth,GetColor(15,15,15),hdxfont(fNoto_m30)
						//アーティスト名
							DrawStringXCenterToHandle -128+tmpease,384,Libarr_Artist(CursorLevID,CursorSelID),DispWidth,GetColor(30,30,30),hdxfont(fNoto_m16)
						//難易度
							sdim tmpstr:tmpstr=Libarr_Difficulty(CursorLevID,CursorSelID)+" Lv."+Libarr_Level(CursorLevID,CursorSelID)
							DrawStringXCenterToHandle 128-tmpease,428,tmpstr,DispWidth,GetDifficultyColor(Libarr_Difficulty(CursorLevID,CursorSelID)),hdxfont(fGood_14)

					//暗転
						if cnt>60*DecideWaitTime-20 {
							setease 0,256,ease_linear
							SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(cnt-(60*5-20),20-1)
							DrawBox 0,0,DispWidth,DispHeight,GetColor(0,0,0),TRUE
						}

				ScreenFlip
				await
			loop
			PauseMovieToGraph hdximg(iMovie_Decide)

			//黒背景
				SetDrawScreen DX_SCREEN_FRONT
				SetDrawBlendMode DX_BLENDMODE_NOBLEND,256
				DrawBox 0,0,DispWidth,DispHeight,GetColor(0,0,0),1
			//ゲームプレイに移行
				gosub*Scene_Free_MusicSelect
				BASS_ChannelStop hbasssnd(bgmMain)
				sdim chartFileName
				chartFileName=Libarr_ChartID(CursorLevID,CursorSelID)
				gosub*LoadChart
				gosub*Scene_Init_Play
				curSceneID=ID_Play
				gosub*InitScene
				PlayStartMS=GetNowCount()
		}
		if SceneCount==10 :PlaySE hbasssnd(vocMusicSel)
		if JumpSceneFlag==TRUE :JumpSceneCount++
		if JumpSceneCount>20 {
			gosub*Scene_Free_MusicSelect
			BASS_ChannelSetAttribute hbasssnd(bgmMain),BASS_ATTRIB_VOL,tofloat(0.7)
			switch JumpSceneID
				case ID_MusicSelect_Option:gosub*Scene_Init_MusicSelect_Option:swbreak
				case ID_MusicSelect_Effect:gosub*Scene_Init_MusicSelect_Effect:swbreak
				case ID_MusicSelect_Partner:gosub*Scene_Init_MusicSelect_Partner:swbreak
			swend
			curSceneID=JumpSceneID
			gosub*InitScene
		}

	return

*Scene_Free_MusicSelect
	//セーブデータの同期
		aqMusicSelect_SelID=CursorSelID
		aqMusicSelect_LevID=CursorLevID
		SendProfile"XODA.MUSICSELECT.ID",str(CursorSelID)
		SendProfile"XODA.MUSICSELECT.LEVEL",str(CursorLevID)

	//エフェクトの削除
		repeat 3*Libarr_Max
			BASS_ChannelRemoveFX hBGMpreview(cnt),hBGMFX(cnt)
			BASS_FXReset hBGMFX(cnt)
			BASS_ChannelStop hBGMpreview(cnt)
		loop
	return

*Scene_Init_MusicSelect_Option

	//変数初期化
	dim ItemCursorPos		//カーソル位置
	dim ChangeItemFlag,9	//値を変更したフラグ

	return

*Scene_MusicSelect_Option

	//キー判定

		if KeyFlag(KEY_INPUT_LEFT)==TRUE&bfKeyFlag(KEY_INPUT_LEFT)==FALSE {
			aqPlayOption(ItemCursorPos)--
			if aqPlayOption(ItemCursorPos)<0 :aqPlayOption(ItemCursorPos)=Item_NumOptionValue(ItemCursorPos)-1
			ChangeItemFlag(ItemCursorPos)=TRUE
			PlaySE hbasssnd(sndUI_Cursor)
		}
		if KeyFlag(KEY_INPUT_RIGHT)==TRUE&bfKeyFlag(KEY_INPUT_RIGHT)==FALSE {
			aqPlayOption(ItemCursorPos)++
			if aqPlayOption(ItemCursorPos)>Item_NumOptionValue(ItemCursorPos)-1 :aqPlayOption(ItemCursorPos)=0
			ChangeItemFlag(ItemCursorPos)=TRUE
			PlaySE hbasssnd(sndUI_Cursor)
		}
		if KeyFlag(KEY_INPUT_UP)==TRUE&bfKeyFlag(KEY_INPUT_UP)==FALSE {
			ItemCursorPos--
			if ItemCursorPos<0 :ItemCursorPos=8
			PlaySE hbasssnd(sndUI_Cursor)
		}
		if KeyFlag(KEY_INPUT_DOWN)==TRUE&bfKeyFlag(KEY_INPUT_DOWN)==FALSE {
			ItemCursorPos++
			if ItemCursorPos>8 :ItemCursorPos=0
			PlaySE hbasssnd(sndUI_Cursor)
		}

		if (KeyFlag(KEY_INPUT_RETURN)==TRUE|KeyFlag(KEY_INPUT_1)==TRUE)&decideFlag==FALSE {
			decideFlag=TRUE
			decideFrameCount=0
			PlaySE hbasssnd(sndUI_Cancel)
		}

	//背景
		SetDrawBlendMode DX_BLENDMODE_NOBLEND,256
		DrawGraph 0,0,hdximg(iBackground),FALSE

	//プレビュー
		if decideFlag==FALSE {
			setease 100,150,ease_cubic_out
			EaseHeight=getease(SceneCount,20)
			setease 0,256,ease_cubic_out
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount,20)
		} else {
			setease 150,100,ease_cubic_out
			EaseHeight=getease(decideFrameCount,20)
			setease 256,0,ease_cubic_out
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(decideFrameCount,20)
		}
		DrawExtendGraph 0,16+150/2-EaseHeight/2,DispWidth,15+150/2+EaseHeight/2,hdximg(ibg_Option_Preview),TRUE

	//左ウィンドウ
		if decideFlag==FALSE {
			setease -640,32,ease_cubic_out
			EasePosX=getease(SceneCount,25)
		} else {
			setease 32,-640,ease_cubic_out
			EasePosX=getease(decideFrameCount,25)
		}
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph EasePosX,176,hdximg(ibg_Option_Window),TRUE

		//描画
		if decideFlag==FALSE {
			if SceneCount>10 {
				setease 0,256,ease_cubic_out
				SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(SceneCount-10,20)
				setease -64,0,ease_cubic_out
				EasePosX=getease(SceneCount-10,20)
					repeat 9
						DrawStringToHandle 72+EasePosX,193+38*cnt,Item_OptionName(cnt),GetColor(30,30,30),hdxfont(fNoto_m14)
						DrawStringXCenterToHandle 332+EasePosX,193+38*cnt,Item_OptionValue(cnt,aqPlayOption(cnt)),300,GetColor(30,30,30),hdxfont(fNoto_m14)
					loop
					DrawStringToHandle 72+EasePosX,534,Item_OptionValue(ItemCursorPos,aqPlayOption(ItemCursorPos))+" - "+Item_OptionDescription(ItemCursorPos,aqPlayOption(ItemCursorPos)),GetColor(30,30,30),hdxfont(fNoto_m16)
			}
			if SceneCount>20 {
				setease 0,256,ease_linear+ease_loop
				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount-20,10)
				DrawGraph 52,182+38*ItemCursorPos,hdximg(iCursor_Option),TRUE
			}
		}

	//右ウィンドウ
		if decideFlag==FALSE {
			setease DispWidth,780,ease_cubic_out
			EasePosX=getease(SceneCount,25)
		} else {
			setease 780,DispWidth,ease_cubic_out
			EasePosX=getease(decideFrameCount,25)
		}
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph EasePosX,180,hdximg(ibg_Option_SongInfo),TRUE

		SetDrawBlendMode DX_BLENDMODE_ALPHA,256
		//曲名
			DrawStringToHandle EasePosX+820-780,225,Libarr_Title(CursorLevID,CursorSelID),GetColor(40,40,40),hdxfont(fNoto_m24)
		//アーティスト名
			DrawStringToHandle EasePosX+820-780,269,Libarr_Artist(CursorLevID,CursorSelID),GetColor(40,40,40),hdxfont(fNoto_m16)

		//BPM
			DrawStringToHandle EasePosX+858-780,312,strf("%.1f",str(Libarr_DefBPM(CursorLevID,CursorSelID))),GetColor(40,40,40),hdxfont(fNoto_m16)
		//SPEED値
			DrawStringToHandle EasePosX+923-780,312,strf("%.1f",str(0.5+double(aqPlayOption(ID_ObjSpeed))*0.1)),GetColor(40,40,40),hdxfont(fNoto_m16)
		//速度値
			DrawStringToHandle EasePosX+990-780,312,strf("%.2f",str((0.5+double(aqPlayOption(ID_ObjSpeed))*0.1)*Libarr_DefBPM(CursorLevID,CursorSelID))),GetColor(8,181,151),hdxfont(fNoto_m16)

	//プレビュー
		if decideFlag==FALSE {
			//カーソル
				dim hCursorGraph:hCursorGraph=GetCursorHandle()
				if hCursorGraph!-1 {
					SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
					setease -64,100,ease_cubic_out
					DrawGraph getease(SceneCount-25,25),55,hCursorGraph,TRUE
				}
			//オブジェクト
				if SceneCount>25 {
					dim hObjImgArr
					SetObjImgArr hObjImgArr,1

					//ユーザー指定のスピード値
					ddim AddSpeed,1
					AddSpeed=0.5+double(aqPlayOption(ID_ObjSpeed))*0.1
					AddSpeed=AddSpeed*2.0	//補正
					repeat
						setease 0,256,ease_linear
						SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount-25,20)
						hitobjPosition=-100*cnt+(1000.0/60.0*Libarr_DefBPM(CursorLevID,CursorSelID)/1000.0)*(SceneCount-25)
						dim DrawobjPosX:dim DrawobjPosY:dim hitobjSize
						hitobjSize=int(48.0*(double(aqPlayOption(ID_ObjSize)*10+10)/100.0))
						DrawobjPosX=200-(double(hitobjPosition)*AddSpeed)+DispWidth
						DrawobjPosY=91-hitobjSize/2-hitobjSize+hitobjSize*1

						if DrawobjPosX<(-hitobjSize) :continue
						if DrawobjPosX>DispWidth :break

						DrawExtendGraph DrawobjPosX,DrawobjPosY,DrawobjPosX+hitobjSize,DrawobjPosY+hitobjSize,hObjImgArr(HITOBJ_NORMAL),TRUE
					loop
				}
		}

	//プリローダー
		setease 100,0,ease_cubic_out
		EaseWidth=getease(SceneCount-20,20)
		EaseHeight=getease(SceneCount-20,20)
		EasePosX=DispWidth/2-EaseWidth/2
		EasePosY=DispHeight/2-EaseHeight/2
		setease 256,0,ease_cubic_out
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount-20,20)
		DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdxarr_Blocks(FrameCount/2\20),TRUE

		if decideFlag==TRUE {
			setease 0,100,ease_cubic_out
			EaseWidth=getease(decideFrameCount,20)
			EaseHeight=getease(decideFrameCount,20)
			EasePosX=DispWidth/2-EaseWidth/2
			EasePosY=DispHeight/2-EaseHeight/2
			setease 0,256,ease_cubic_out
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(decideFrameCount,20)
			DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdxarr_Blocks(FrameCount/2\20),TRUE
		}

	//ガイド
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph 0,DispHeight-128,hdximg(ibg_GuidePanel),TRUE

		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph 540,650,hdximg(iGuide_Large),TRUE
		DrawGraph 370,660,hdximg(iGuide_Small),TRUE
		DrawGraph 760,660,hdximg(iGuide_Small),TRUE

		SetDrawBlendMode DX_BLENDMODE_ALPHA,256
		sdim tmpstr:tmpstr="ENTER / 1 : 戻る"
		DrawStringXCenterToHandle 540,658,strmid(tmpstr,0,limit(SceneCount,0,strlen(tmpstr))),200,GetColor(230,230,230),hdxfont(fNoto_m14)
		sdim tmpstr:tmpstr="↑↓ : カーソル移動"
		DrawStringXCenterToHandle 370,668,strmid(tmpstr,0,limit(SceneCount,0,strlen(tmpstr))),150,GetColor(230,230,230),hdxfont(fNoto_m14)
		sdim tmpstr:tmpstr="←→ : 設定を変更"
		DrawStringXCenterToHandle 760,668,strmid(tmpstr,0,limit(SceneCount,0,strlen(tmpstr))),150,GetColor(230,230,230),hdxfont(fNoto_m14)

	if decideFlag==TRUE :decideFrameCount++
	if decideFrameCount==10 {
		//設定を保存
		repeat 9
			if ChangeItemFlag(cnt)==TRUE {
				AddDebugConsole STYLE_NORMAL,"プレイオプションを保存しました。[XODA.PLAYOPTION."+(cnt)+"]"
				SendProfile"XODA.PLAYOPTION."+(cnt),str(aqPlayOption(cnt))
			}
		loop
	}
	if decideFrameCount>30 {
		curSceneID=ID_MusicSelect
		gosub*InitScene
		//変数初期化
		dim decideFlag
		dim decideFrameCount
		dim transEffFlag
		gosub*Scene_Init_MusicSelect
	}

	return

*Scene_Init_MusicSelect_Effect

	dim FXitemhwnd,20
	sdim FXitemname,32,20
	sdim FXitemtype,16,20
	dim FXitemflag,20
	sdim FXitemlockmsg,32,20
	sdim FXitemdesc,64,20
	dim FxID,5,4

	repeat 5*4
		FxID(cnt\5,cnt/5)=cnt
	loop

	FXitemhwnd(0)=hdximg(iFX_NOFX),hdximg(iFX_BassBoost),hdximg(iFX_StrongBass),hdximg(iFX_MaximumBass),hdximg(iFX_TrebleBoost)
	FXitemhwnd(5)=hdximg(iFX_2000HzBoost),hdximg(iFX_4000HzBoost),hdximg(iFX_Compressor),hdximg(iFX_ExtraCompressor),hdximg(iFX_Distortion)
	FXitemhwnd(10)=hdximg(iFX_Chorus),hdximg(iFX_Flanger),hdximg(iFX_ExtraFlanger),hdximg(iFX_Echo),hdximg(iFX_ExtraEcho)
	FXitemhwnd(15)=hdximg(iFX_Silent),hdximg(iFX_LoudnessPlus),hdximg(iFX_LoudnessHalf),hdximg(iFX_Reverb),hdximg(iFX_ExtraReverb)

	FXitemname(0)="No Effect","Bass Boost","Strong Bass","Maximum Bass","Treble Boost"
	FXitemname(5)="2000Hz Boost","4000Hz Boost","Compressor","Extra Compressor","Distortion"
	FXitemname(10)="Chorus","Flanger","Extra Flanger","Echo","Extra Echo"
	FXitemname(15)="Silent","Loudness Plus+","Loudness Half","Reverb","Extra Reverb"

	FXitemtype(0)="なし","Equalizer","Equalizer","Equalizer","Equalizer"
	FXitemtype(5)="Equalizer","Equalizer","Compressor","Compressor","Distortion"
	FXitemtype(10)="Delay","Delay","Delay","Delay","Delay"
	FXitemtype(15)="Amplifier","Amplifier","Amplifier","Room","Room"

	FXitemflag=1,1,0,0,1,0,0,1,0,1,1,1,0,1,0,0,0,0,1,0
	if AllUnlockEffect==TRUE {
		repeat 20
			FXitemflag(cnt)=TRUE
		loop
	}

	FXitemlockmsg(FxID(2,0))="Bass Boostを使用してAランク以上を達成する。"
	FXitemlockmsg(FxID(3,0))="Strong Boostを使用してAAランク以上を達成する。"
	FXitemlockmsg(FxID(0,1))="プレイ回数が10回以上になる。"
	FXitemlockmsg(FxID(1,1))="プレイ回数が20回以上になる。"
	FXitemlockmsg(FxID(3,1))="Compressorを使用してAAランク以上を達成する。"
	FXitemlockmsg(FxID(2,2))="いずれかの楽曲でMadnessクリアを達成する。"
	FXitemlockmsg(FxID(4,2))="いずれかの楽曲でSランク以上を達成する。"
	FXitemlockmsg(FxID(0,3))="キーサウンドを0%にして楽曲をクリアする。"
	FXitemlockmsg(FxID(1,3))="いずれかの楽曲でFull Comboを達成する。	"
	FXitemlockmsg(FxID(2,3))="いずれかの楽曲でALL Excellentを達成する。"
	FXitemlockmsg(FxID(4,3))="Reverbを使用してAAAランク以上を達成する。"

	FXitemdesc(0)="エフェクターを無効化します。"
	FXitemdesc(1)="サウンドの低音域をブーストします。	"
	FXitemdesc(2)="サウンドの低音域を大きくブーストします。"
	FXitemdesc(3)="サウンドの低音域を最大限にブーストします。"
	FXitemdesc(4)="サウンドの高音域をブーストします。"
	FXitemdesc(5)="サウンドの2000Hz帯域をブーストします。"
	FXitemdesc(6)="サウンドの4000Hz帯域をブーストします。"
	FXitemdesc(7)="サウンドの音量を圧縮させます。"
	FXitemdesc(8)="サウンドの音量を大きく圧縮させます。"
	FXitemdesc(9)="サウンドに歪みを発生させます。"
	FXitemdesc(10)="サウンドに原音のディレイを重ねます。"
	FXitemdesc(11)="サウンドに原音の細かいディレイを重ねます。"
	FXitemdesc(12)="サウンドに原音の細かいディレイを大きく重ねます。"
	FXitemdesc(13)="サウンドに残響を発生させます。"
	FXitemdesc(14)="サウンドに残響を多く発生させます。"
	FXitemdesc(15)="サウンドをすべて消去させます。"
	FXitemdesc(16)="サウンドの音量を全体的に増幅させます。"
	FXitemdesc(17)="サウンドの音量を全体的に減少させます。"
	FXitemdesc(18)="サウンドに空間的な残響を発生させます。"
	FXitemdesc(19)="サウンドに空間的な残響を多く発生させます。"

	repeat 20
		if FXitemflag(cnt)==FALSE {
			FXitemname(cnt)="?????"
			FXitemhwnd(cnt)=hdximg(iFX_locked)
			FXitemtype(cnt)="?????"
			FXitemdesc(cnt)="？？？？？"
		}
		if FXitemlockmsg(cnt)=="" :FXitemlockmsg(cnt)="なし"
	loop

	dim SelFXColumn
	dim SelFXLine
	dim EnableFXColumn
	dim EnableFXLine
	dim CursorEasePosX
	dim CursorEasePosY
		EnableFXColumn=aqFxid\5
		EnableFXLine=aqFxid/5
		SelFXColumn=EnableFXColumn
		SelFXLine=EnableFXLine

	return

*Scene_MusicSelect_Effect

	//キー判定
		if KeyFlag(KEY_INPUT_LEFT)==TRUE&bfKeyFlag(KEY_INPUT_LEFT)==FALSE {
			SelFXColumn--
			CursorEasePosX=100
			CursorEasePosY=0
			if SelFXColumn<0 {
				CursorEasePosX=-100*4
				SelFXColumn=4
			}
			PlaySE hbasssnd(sndUI_Cursor)
			CursorSelCount=0
		}
		if KeyFlag(KEY_INPUT_RIGHT)==TRUE&bfKeyFlag(KEY_INPUT_RIGHT)==FALSE {
			SelFXColumn++
			CursorEasePosX=-100
			CursorEasePosY=0
			if SelFXColumn>4 {
				CursorEasePosX=100*4
				SelFXColumn=0
			}
			PlaySE hbasssnd(sndUI_Cursor)
			CursorSelCount=0
		}
		if KeyFlag(KEY_INPUT_UP)==TRUE&bfKeyFlag(KEY_INPUT_UP)==FALSE {
			SelFXLine--
			CursorEasePosX=0
			CursorEasePosY=100
			if SelFXLine<0 {
				CursorEasePosY=-100*3
				SelFXLine=3
			}
			PlaySE hbasssnd(sndUI_Cursor)
			CursorSelCount=0
		}
		if KeyFlag(KEY_INPUT_DOWN)==TRUE&bfKeyFlag(KEY_INPUT_DOWN)==FALSE {
			SelFXLine++
			CursorEasePosX=0
			CursorEasePosY=-100
			if SelFXLine>3 {
				CursorEasePosY=100*3
				SelFXLine=0
			}
			PlaySE hbasssnd(sndUI_Cursor)
			CursorSelCount=0
		}
		if KeyFlag(KEY_INPUT_RETURN)==TRUE&bfKeyFlag(KEY_INPUT_RETURN)==FALSE {
			if FXitemflag(FxID(SelFXColumn,SelFXLine))==FALSE {
				PlaySE hbasssnd(sndUI_Error)

				//通知
				CreateNotificationBuf hdximg(iLayer_Notice),ID_INFORMATION,"このエフェクトは解放されていません。","解放条件:"+FXitemlockmsg(FxID(SelFXColumn,SelFXLine))
				SendNotification hdximg(iLayer_Notice)
				SetDrawScreen DX_SCREEN_BACK
			} else {
				EnableFXColumn=SelFXColumn
				EnableFXLine=SelFXLine
				SetAudioFX bgmMain,FxID(EnableFXColumn,EnableFXLine)
				PlaySE hbasssnd(sndUI_Decide)

				//通知
				if EnableFXColumn==0&EnableFXLine==0 {
					CreateNotificationBuf hdximg(iLayer_Notice),ID_INFORMATION,"エフェクトをOFFにしました。","オーディオエフェクトを変更しました。"
				} else {
					CreateNotificationBuf hdximg(iLayer_Notice),ID_INFORMATION,FXitemname(FxID(EnableFXColumn,EnableFXLine))+"を有効にしました。","オーディオエフェクトを変更しました。"
				}
				SendNotification hdximg(iLayer_Notice)
				SetDrawScreen DX_SCREEN_BACK
			}
		}
		if KeyFlag(KEY_INPUT_SPACE)==TRUE&bfKeyFlag(KEY_INPUT_SPACE)==FALSE {
			repeat
				SelFXColumn=rnd(5)
				SelFXLine=rnd(4)
				if FXitemflag(FxID(SelFXColumn,SelFXLine))==FALSE :continue
				break
			loop
			PlaySE hbasssnd(sndUI_Decide)
		}
		if KeyFlag(KEY_INPUT_2)==TRUE&bfKeyFlag(KEY_INPUT_2)==FALSE {
			decideFlag=TRUE
			PlaySE hbasssnd(sndUI_Cancel)
		}

	//背景
		SetDrawBlendMode DX_BLENDMODE_NOBLEND,256
		DrawGraph 0,0,hdximg(iBackground),FALSE

	//ナビゲーション
		if decideFlag==FALSE {
			setease 0,256,ease_cubic_out
			EaseAlpha=getease(SceneCount,10)
			setease -32,0,ease_cubic_out
			EasePosX=getease(SceneCount,10)
		} else {
			setease 256,0,ease_cubic_out
			EaseAlpha=getease(decideFrameCount-20,10)
			setease 0,-32,ease_cubic_out
			EasePosX=getease(decideFrameCount-20,10)
		}
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,EaseAlpha
		DrawGraph 32+EasePosX,16,hdximg(iNav_Effect),TRUE

	//案内文字列（背景）
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph 32,67,hdximg(ibg_Nav),TRUE

	//案内文字列
		SetDrawBlendMode DX_BLENDMODE_ALPHA,256
		DrawStringToHandle 48,72,"使用するオーディオエフェクトを選択してください。",GetColor(230,230,230),hdxfont(fNoto_b12)


	//ウィンドウ背景
		if decideFlag==FALSE {
			setease 200,256,ease_cubic_out
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount,30)
			setease -DispWidth,0,ease_cubic_out
			DrawGraph getease(SceneCount,30),113,hdximg(ibg_Effect),TRUE
		} else {
			setease 256,0,ease_cubic_out
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(decideFrameCount-15,25)
			setease 0,DispWidth,ease_cubic_out
			DrawGraph getease(decideFrameCount-15,25),113,hdximg(ibg_Effect),TRUE
		}

	//エフェクトのアイコン
		dim DrawIconColumn:dim DrawIconLine
		repeat 5
			DrawIconColumn=cnt
			repeat 4
				DrawIconLine=cnt
				setease 0,80,ease_cubic_out
				EaseWidth=getease(SceneCount-20-DrawIconColumn*3,20)
				if decideFlag==TRUE {
					EaseWidth=80-getease(decideFrameCount-DrawIconColumn*3,20)
				}
				EaseHeight=EaseWidth
				EasePosX=72+100*DrawIconColumn
				EasePosY=140+100*DrawIconLine

				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
				if FXitemFlag(FxID(DrawIconColumn,DrawIconLine))==FALSE {
					setease 256,150,ease_linear+ease_loop
					SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount,40)
				}

				DrawExtendGraph EasePosX+80/2-EaseWidth/2,EasePosY+80/2-EaseHeight/2,EasePosX+80/2+EaseWidth/2,EasePosY+80/2+EaseHeight/2,FXitemhwnd(FxID(DrawIconColumn,DrawIconLine)),TRUE
			loop
		loop

	//文字列
		if SceneCount<30|decideFlag==TRUE :CursorSelCount=0
		setease 0,256,ease_cubic_out
		SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(CursorSelCount,20)
		setease -64,0,ease_cubic_out
		DrawStringToHandle 700+getease(CursorSelCount,20),200,FXitemname(FxID(SelFXColumn,SelFXLine)),GetColor(240,240,240),hdxfont(fPier_r32)
		DrawStringToHandle 700+getease(CursorSelCount,20),278,"種類",GetColor(200,200,200),hdxfont(fNoto_m16)
		DrawStringToHandle 700+getease(CursorSelCount,20),310,"解放条件",GetColor(200,200,200),hdxfont(fNoto_m16)
		DrawStringToHandle 700+getease(CursorSelCount,20),342,"説明",GetColor(200,200,200),hdxfont(fNoto_m16)
		DrawStringToHandle 800+getease(CursorSelCount,20),278,FXitemtype(FxID(SelFXColumn,SelFXLine)),GetColor(200,200,200),hdxfont(fNoto_m16)
		DrawStringToHandle 800+getease(CursorSelCount,20),310,FXitemlockmsg(FxID(SelFXColumn,SelFXLine)),GetColor(200,200,200),hdxfont(fNoto_m16)
		DrawStringToHandle 800+getease(CursorSelCount,20),342,FXitemdesc(FxID(SelFXColumn,SelFXLine)),GetColor(200,200,200),hdxfont(fNoto_m16)

	//ビジュアライザー
		if decideFlag==FALSE {
			dim bgmFFTbuf,4096:dim bgmFFTline
			BASS_ChannelGetData hbasssnd(bgmMain),varptr(bgmFFTbuf),BASS_DATA_FFT8192

			SetDrawBlendMode DX_BLENDMODE_ALPHA,100
			repeat 32
				bgmFFTline=int(todouble(bgmFFTbuf(128/32*cnt+100))*2500)
				if bgmFFTline>128 :bgmFFTline=128
				DrawBox 640+cnt*(600/32),392+128-bgmFFTline,640+(cnt+1)*(600/32)-2,392+128,GetColor(255,255,255),TRUE
			loop
		}

	//ステータス
		if decideFlag==FALSE {
			setease 0,256,ease_linear
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount-10,10)
		} else {
			setease 256,0,ease_linear
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(decideFrameCount-10,10)
		}
		DrawGraph 730,32,hdximg(iMixed_PlayerStatus),TRUE

	if decideFlag==FALSE {
		//有効
			if SceneCount>20 {
				setease 0,256,ease_linear
				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount-20,15)
				DrawGraph 66+100*EnableFXColumn,134+100*EnableFXLine,hdximg(iFX_cursor),TRUE
			}

		//カーソル
			setease 0,150,ease_linear
				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount-20,15)
			setease 1.1,1,ease_cubic_inout+ease_loop
				EaseWidth=int(92.0*geteasef(CursorSelCount,25)):EaseHeight=EaseWidth
			setease CursorEasePosX,0,ease_cubic_out
				EasePosX=66+100*SelFXColumn+92/2-EaseWidth/2+getease(CursorSelCount,8)
			setease CursorEasePosY,0,ease_cubic_out
				EasePosY=134+100*SelFXLine+92/2-EaseHeight/2+getease(CursorSelCount,8)
				DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdximg(iFX_Cursor),TRUE
	}

	//ガイド
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph 0,DispHeight-128,hdximg(ibg_GuidePanel),TRUE

		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph 540,650,hdximg(iGuide_Large),TRUE
		DrawGraph 370,640,hdximg(iGuide_Small),TRUE
		DrawGraph 760,640,hdximg(iGuide_Small),TRUE
		DrawGraph 370,676,hdximg(iGuide_Small),TRUE
		DrawGraph 760,676,hdximg(iGuide_Small),TRUE

		SetDrawBlendMode DX_BLENDMODE_ALPHA,256
		sdim tmpstr:tmpstr="ENTER : 決定"
		DrawStringXCenterToHandle 540,658,strmid(tmpstr,0,limit(SceneCount,0,strlen(tmpstr))),200,GetColor(230,230,230),hdxfont(fNoto_m14)
		sdim tmpstr:tmpstr="2 : 戻る"
		DrawStringXCenterToHandle 370,648,strmid(tmpstr,0,limit(SceneCount,0,strlen(tmpstr))),150,GetColor(230,230,230),hdxfont(fNoto_m14)
		sdim tmpstr:tmpstr="SPACE : ランダム"
		DrawStringXCenterToHandle 760,648,strmid(tmpstr,0,limit(SceneCount,0,strlen(tmpstr))),150,GetColor(230,230,230),hdxfont(fNoto_m14)
		sdim tmpstr:tmpstr="↑↓ : カーソル移動"
		DrawStringXCenterToHandle 370,684,strmid(tmpstr,0,limit(SceneCount,0,strlen(tmpstr))),150,GetColor(230,230,230),hdxfont(fNoto_m14)
		sdim tmpstr:tmpstr="←→ : カーソル移動"
		DrawStringXCenterToHandle 760,684,strmid(tmpstr,0,limit(SceneCount,0,strlen(tmpstr))),150,GetColor(230,230,230),hdxfont(fNoto_m14)

	//プリローダー
		setease 100,0,ease_cubic_out
		EaseWidth=getease(SceneCount,20)
		EaseHeight=getease(SceneCount,20)
		EasePosX=DispWidth/2-EaseWidth/2
		EasePosY=DispHeight/2-EaseHeight/2
		setease 256,0,ease_cubic_out
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount,20)
		DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdxarr_Blocks(FrameCount/2\20),TRUE

		if decideFlag==TRUE {
			setease 0,100,ease_cubic_out
			EaseWidth=getease(decideFrameCount-20,20)
			EaseHeight=getease(decideFrameCount-20,20)
			EasePosX=DispWidth/2-EaseWidth/2
			EasePosY=DispHeight/2-EaseHeight/2
			setease 0,256,ease_cubic_out
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(decideFrameCount-20,20)
			DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdxarr_Blocks(FrameCount/2\20),TRUE
		}

	//カウンター
		CursorSelCount++
		if decideFlag==TRUE :decideFrameCount++
		if decideFrameCount>35 {
			aqFxid=FxID(EnableFXColumn,EnableFXLine)
			SendProfile"XODA.AUDIOFX",str(aqFxid)
			curSceneID=ID_MusicSelect
			gosub*InitScene
			//変数初期化
			dim decideFlag
			dim decideFrameCount
			dim transEffFlag
			gosub*Scene_Init_MusicSelect
		}
	return

*Scene_Init_MusicSelect_Partner

	//変数初期化
		dim decideFlag
		dim decideFrameCount
		dim transEffFlag

	//カーソル位置
		dim CardCursorID
		dim CardCursorCount
		dim CardCursorEase
		CardCursorID=pnCurID

	return

*Scene_MusicSelect_Partner

	//キー判定
		if KeyFlag(KEY_INPUT_LEFT)==TRUE&bfKeyFlag(KEY_INPUT_LEFT)==FALSE {
			CardCursorID--
			CardCursorEase=-(220+32)
			if CardCursorID<0 :CardCursorID=0:CardCursorEase=0
			CardCursorCount=0
			PlaySE hbasssnd(sndUI_Cursor)
		}
		if KeyFlag(KEY_INPUT_RIGHT)==TRUE&bfKeyFlag(KEY_INPUT_RIGHT)==FALSE {
			CardCursorID++
			CardCursorEase=(220+32)
			if CardCursorID>=pnMax :CardCursorID=pnMax:CardCursorEase=0
			CardCursorCount=0
			PlaySE hbasssnd(sndUI_Cursor)
		}
		if ((KeyFlag(KEY_INPUT_RETURN)==TRUE&bfKeyFlag(KEY_INPUT_RETURN)==FALSE)|(KeyFlag(KEY_INPUT_3)==TRUE&bfKeyFlag(KEY_INPUT_3)==FALSE))&decideFlag==FALSE {
			if pnUnlockFlag(CardCursorID)==FALSE {
				//未解放
				PlaySE hbasssnd(sndUI_Error)
				//通知
				CreateNotificationBuf hdximg(iLayer_Notice),ID_INFORMATION,"このパートナーは解放されていません。",pnName(CardCursorID)+"は使用できません。"
				SendNotification hdximg(iLayer_Notice)
				SetDrawScreen DX_SCREEN_BACK
			} else {
				decideFlag=TRUE
				decideFrameCount=0
				PlaySE hbasssnd(sndUI_Decide)
			}
		}

	//背景&レイヤー
		SetDrawBlendMode DX_BLENDMODE_NOBLEND,256
		DrawGraph 0,0,hdximg(iBackground),FALSE

	//ナビゲーション
		if decideFlag==FALSE {
			setease 0,256,ease_cubic_out
			EaseAlpha=getease(SceneCount,10)
			setease -32,0,ease_cubic_out
			EasePosX=getease(SceneCount,10)
		} else {
			setease 256,0,ease_cubic_out
			EaseAlpha=getease(decideFrameCount-20,10)
			setease 0,-32,ease_cubic_out
			EasePosX=getease(decideFrameCount-20,10)
		}
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,EaseAlpha
		DrawGraph 32+EasePosX,16,hdximg(iNav_Partner),TRUE

	//案内文字列（背景）
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph 32,67,hdximg(ibg_Nav),TRUE

	//案内文字列
		SetDrawBlendMode DX_BLENDMODE_ALPHA,256
		DrawStringToHandle 48,72,"使用するパートナーを選択してください。",GetColor(230,230,230),hdxfont(fNoto_b12)

	//----以下、シーン別レイヤー---
		SetDrawScreen hdximg(iLayer_Scene)
		ClearDrawScreen

		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph 0,132,hdximg(ibg_Partner),TRUE
		DrawRotaGraph 640,332,1.0,deg2rad(double(SceneCount)*2.0\360.0),hdximg(iLayer_Partner_Circle),TRUE,FALSE

		//ステータス
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph 730,32,hdximg(iMixed_PlayerStatus),TRUE

		//カード一覧
			repeat pnMax
				setease CardCursorEase,0,ease_cubic_out
				EasePosX=getease(CardCursorCount,14)
				if cnt==CardCursorID {
					setease 0,16,ease_cubic_out
					EasePosY=getease(CardCursorCount,14)
				} else :EasePosY=0

				//描画
				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
				if pnUnlockFlag(cnt)==TRUE {
					DrawGraph 530+(cnt-CardCursorID)*(220+32)+EasePosX,216-EasePosY,pnhCard(cnt),TRUE
					SetDrawBlendMode DX_BLENDMODE_ALPHA,256
					DrawStringToHandle 530+(cnt-CardCursorID)*(220+32)+EasePosX+156,216-EasePosY+207,"Lv. "+pnLevel(cnt),GetColor(10,10,10),hdxfont(fPier_r14)
				} else {
					DrawGraph 530+(cnt-CardCursorID)*(220+32)+EasePosX,216-EasePosY,hdximg(iIcon_Partner_Locked),TRUE
				}
				if pnAdv(cnt)==TRUE {
					SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,200
					DrawGraph 530+(cnt-CardCursorID)*(220+32)+EasePosX,216-EasePosY,hdxarr_CardAdvsign(SceneCount\4),TRUE
				}
			loop

		//数
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph 590,514,hdximg(iLayer_Partner_Num),TRUE
			SetDrawBlendMode DX_BLENDMODE_ALPHA,256
			DrawStringXCenterToHandle 590,518,str(CardCursorID+1)+"/"+pnMax,100,GetColor(30,30,30),hdxfont(fPier_m16)

	//シーン別レイヤー
		SetDrawScreen DX_SCREEN_BACK
			if decideFlag==FALSE {
				setease 1.2,1,ease_cubic_out
				EaseWidth=int(double(DispWidth)*geteasef(SceneCount,20))
				EaseHeight=int(double(DispHeight)*geteasef(SceneCount,20))
				EasePosX=DispWidth/2-EaseWidth/2
				EasePosY=DispHeight/2-EaseHeight/2
				setease 0,256,ease_cubic_out
				SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(SceneCount,20)
				DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdximg(iLayer_Scene),TRUE
			} else {
				setease 1,1.2,ease_cubic_out
				EaseWidth=int(double(DispWidth)*geteasef(decideFrameCount,20))
				EaseHeight=int(double(DispHeight)*geteasef(decideFrameCount,20))
				EasePosX=DispWidth/2-EaseWidth/2
				EasePosY=DispHeight/2-EaseHeight/2
				setease 256,0,ease_cubic_out
				SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(decideFrameCount,20)
				DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdximg(iLayer_Scene),TRUE

				//プリローダー
				setease 0,100,ease_cubic_out
				EaseWidth=getease(decideFrameCount,20)
				EaseHeight=getease(decideFrameCount,20)
				EasePosX=DispWidth/2-EaseWidth/2
				EasePosY=DispHeight/2-EaseHeight/2
				setease 0,256,ease_cubic_out
				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(decideFrameCount,20)
				DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdxarr_Blocks(FrameCount/2\20),TRUE
			}

	//プリローダー
		setease 100,0,ease_cubic_out
		EaseWidth=getease(SceneCount-20,20)
		EaseHeight=getease(SceneCount-20,20)
		EasePosX=DispWidth/2-EaseWidth/2
		EasePosY=DispHeight/2-EaseHeight/2
		setease 256,0,ease_cubic_out
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount-20,20)
		DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdxarr_Blocks(FrameCount/2\20),TRUE

		if decideFlag==TRUE {
			setease 0,100,ease_cubic_out
			EaseWidth=getease(decideFrameCount,20)
			EaseHeight=getease(decideFrameCount,20)
			EasePosX=DispWidth/2-EaseWidth/2
			EasePosY=DispHeight/2-EaseHeight/2
			setease 0,256,ease_cubic_out
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(decideFrameCount,20)
			DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdxarr_Blocks(FrameCount/2\20),TRUE
		}

	//ガイド
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph 0,DispHeight-128,hdximg(ibg_GuidePanel),TRUE

		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph 540,650,hdximg(iGuide_Large),TRUE
		DrawGraph 370,660,hdximg(iGuide_Small),TRUE
		DrawGraph 760,660,hdximg(iGuide_Small),TRUE

		SetDrawBlendMode DX_BLENDMODE_ALPHA,256
		sdim tmpstr:tmpstr="3 / Enter : 戻る"
		DrawStringXCenterToHandle 540,658,strmid(tmpstr,0,limit(SceneCount,0,strlen(tmpstr))),200,GetColor(230,230,230),hdxfont(fNoto_m14)
		sdim tmpstr:tmpstr="← : カーソル移動"
		DrawStringXCenterToHandle 370,668,strmid(tmpstr,0,limit(SceneCount,0,strlen(tmpstr))),150,GetColor(230,230,230),hdxfont(fNoto_m14)
		sdim tmpstr:tmpstr="→ : カーソル移動"
		DrawStringXCenterToHandle 760,668,strmid(tmpstr,0,limit(SceneCount,0,strlen(tmpstr))),150,GetColor(230,230,230),hdxfont(fNoto_m14)

	//カウンター
		CardCursorCount++
		if decideFlag==TRUE :decideFrameCount++
		if decideFrameCount>25 {
			SendProfile"XODA.PARTNER.SELECT",str(pnID(CardCursorID))
			pnCurID=CardCursorID
			curSceneID=ID_MusicSelect
			gosub*InitScene
			//変数初期化
			dim decideFlag
			dim decideFrameCount
			dim transEffFlag
			gosub*Scene_Init_MusicSelect
		}
	return