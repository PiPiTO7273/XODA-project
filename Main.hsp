
//  __   __  _______  ______   _______             _______  ______   _______      ___  _______  _______  _______
// |  |_|  ||       ||      | |   _   |           |       ||    _ | |       |    |   ||       ||       ||       |
// |       ||   _   ||  _    ||  |_|  |   ____    |    _  ||   | || |   _   |    |   ||    ___||       ||_     _|
// |       ||  | |  || | |   ||       |  |____|   |   |_| ||   |_|| |  | |  |    |   ||   |___ |       |  |   |
//  |     | |  |_|  || |_|   ||       |           |    ___||    __ ||  |_|  |    |   ||    ___||      _|  |   |
// |   _   ||       ||       ||   _   |           |   |    |   |  |||       |   |    ||   |___ |     |_   |   |
// |__| |__||_______||______| |__| |__|           |___|    |___|  |||_______|   |____||_______||_______|  |___|

//(c) 2019-2021 PiPiTO Software.

//Official Webpage		https://pipito7273.github.io/
//Github Repository		https://github.com/PiPiTO7273/XODA-project
//Contact				pipito7273@gmail.com

//	-	ソースコードはオープンソース化されていますが、セキュリティ上の問題がある情報（サーバーに接続するユーザー名やパスワード）は
//		外部のソースコードにマクロとして定義されています。これらの情報が欠損している場合でも起動は可能ですが、全ての機能は利用できません。
//		全ての機能をテスト、デバッグするには開発メンバーに連絡し、該当のファイル"AdminKey.hsp"を入手してください。

//	-	開発メンバーの方へ
//		Portalアカウントに接続する際、以下のアカウントを使用してください。
//		ユーザー名	:	devuser
//		パスワード	:	password

#packopt hide 1

//----- モジュール -----
#include"hsp3utf.as"				//UTF-8対応ランタイム
#include"a2dp.hsp"					//Artlet2D + Artlet2D Plus
#include"DxLib.as"					//DXライブラリ
#include"mod_dxlibplus.hsp"			//DXライブラリ拡張モジュール
#include"bass.as"					//BASS Audio Library
#include"hspinet.as"				//hspinet プラグイン
#include"gm_crlf.hsp"				//改行コード変換モジュール
#include"llmod3/llmod3.hsp"			//llmod3基本モジュール
#include"llmod3/console.hsp"			//llmod3コンソール機能モジュール
#include"getProcess.hsp"				//プロセス一覧取得モジュール
#include"hspext.as"					//hspext プラグイン
#addition"AdminKey.hsp"				//セキュリティキー
#include"Symbol.hsp"				//マクロ定義
#include"Function_Default.hsp"		//基本命令・関数
#include"Function_MusicSelect.hsp"		//楽曲選択画面の命令・関数
#include"Function_Play.hsp"			//ゲームプレイ画面の命令・関数
#include"LoadResources.hsp"			//リソース処理関連のサブルーチン
#include"Scene_Splash.hsp"			//スプラッシュ（起動時の待機画面）
#include"Scene_Signin.hsp"			//サインイン
#include"Scene_ModeSelect.hsp"		//モード選択
#include"Scene_MusicSelect.hsp"		//楽曲選択
#include"Scene_Play.hsp"				//ゲームプレイ

//----- 起動開始 -----
*Startup

	#ifdef _debug
		gsel 0,-1
		exist"Main.ax"
		if strsize!-1 {
			delete"Main.ax"
		}
		console
	#endif

	sdim ConsoleLogBuf

	//起動時のアスキーアート
	AddDebugConsole STYLE_COMMENT,"  __   __  _______  ______   _______ "
	AddDebugConsole STYLE_COMMENT," |  |_|  ||       ||      | |   _   |"
	AddDebugConsole STYLE_COMMENT," |       ||   _   ||  _    ||  |_|  |"
	AddDebugConsole STYLE_COMMENT," |       ||  | |  || | |   ||       |    XODA-Project"
	AddDebugConsole STYLE_COMMENT,"  |     | |  |_|  || |_|   ||       |    (c) 2019-"+gettime(0)+" PiPiTO Software."
	AddDebugConsole STYLE_COMMENT," |   _   ||       ||       ||   _   |"
	AddDebugConsole STYLE_COMMENT," |__| |__||_______||______| |__| |__|"
	AddDebugConsole STYLE_COMMENT,""
	AddDebugConsole STYLE_COMMENT,"::::::::::::::::::::::::::::::::::::: :::: ::: :: :  :   :    :\n\n"

	AddDebugConsole STYLE_NORMAL,"Startup..."

	AddDebugConsole STYLE_STATUS,"DISPLAY.WIDTH = "+DispWidth
	AddDebugConsole STYLE_STATUS,"DISPLAY.HEIGHT = "+DispHeight
	AddDebugConsole STYLE_STATUS,"BUILD.VERSION = "+BuildVersion_Major+"."+BuildVersion_Minor
	AddDebugConsole STYLE_STATUS,"SERVICE.HOST = "+RepHiddenWords(str(SERVICE_HOST),"*")
	AddDebugConsole STYLE_STATUS,"SERVICE.USERNAME = "+RepHiddenWords(str(SERVICE_USERNAME),"*")
	AddDebugConsole STYLE_STATUS,"SERVICE.PASSWORD = "+RepHiddenWords(str(SERVICE_PASSWORD),"*")
	AddDebugConsole STYLE_NORMAL,"起動設定の読み込み完了"

	//DXライブラリ初期化
	AddDebugConsole STYLE_NORMAL,"DXライブラリの初期化開始"
	SetOutApplicationLogValidFlag 0
	screen 0,DispWidth,DispHeight,2,ginfo(20)/2-DispWidth/2,ginfo(21)/2-DispHeight/2
	SetUserWindow hwnd
	SetMainWindowText"XODA v"+BuildVersion_Major+"."+BuildVersion_Minor
	ChangeWindowMode TRUE
	SetGraphMode DispWidth,DispHeight,32
	SetAlwaysRunFlag TRUE
	SetWaitVSyncFlag TRUE
	dxlib_Init
	if stat==-1 {
		AddDebugConsole STYLE_ERROR,"DXライブラリの初期化に失敗しました。"
		goto*StopError
	} else {
		AddDebugConsole STYLE_NORMAL,"DXライブラリの初期化完了"
	}
	onexit*Exit

	//BASS初期化
	BASS_Init -1,44100,0,hwnd,0
	if stat==0 {
		AddDebugConsole STYLE_ERROR,"BASS Audio Libraryの初期化に失敗しました。"
	} else {
		AddDebugConsole STYLE_NORMAL,"BASS Audio Libraryの初期化完了"
	}

	//オンラインアップデート
	gosub*CheckOnlineUpdate

	//各種読み込み処理
	gosub*LoadSoundEffects
	gosub*LoadFontData
	gosub*LoadImages

	//シーンID
	dim curSceneID
	curSceneID=ID_Splash
	//現在のFPS値
	ddim curFPSval,1
	//フレームカウンタ
	dim FrameCount

	//////debug
	//読み込む譜面ファイル名
	sdim chartFileName
	chartFileName="0008"
	gosub*InitScene

	gsel 0,1

*MainLoop

	SetDrawScreen DX_SCREEN_BACK
	ClearDrawScreen

	//シーン別描画
	switch curSceneID
		case ID_Splash		//スプラッシュ
			gosub*Scene_Splash
		swbreak
		case ID_Signin		//サインイン
			gosub*Scene_Signin
		swbreak
		case ID_ModeSelect	//モード選択
			gosub*Scene_ModeSelect
		swbreak
		case ID_MusicSelect	//曲選択
			gosub*Scene_MusicSelect
		swbreak
		case ID_Play		//プレイ
			gosub*Scene_Play
		swbreak
	swend

	//FPS表示
	if FrameCount\4==0 {
		GetFPS:getdouble:curFPSval=refdval
	}
	;SetDrawBlendMode DX_BLENDMODE_ALPHA,256
	;DrawStringToHandle DispWidth-256,16,"FPS "+strf("%.3f",curFPSval),GetColor(0,0,0),hdxfont(fSQ_16)

	//カウンタ処理
	SceneCount++
	FrameCount++

	ScreenFlip
	await

	goto*MainLoop

*StopError
	AddDebugConsole STYLE_ERROR,"処理を全て停止しました。"
	AddDebugConsole STYLE_STATUS,"終了待機中"
	dxlib_End
	BASS_Free
	dialog"エラーが発生したため、実行中の全ての処理を停止しました。",1,"XODA"
	stop

*ExitProg
	dxlib_End
	BASS_Free
	AddDebugConsole STYLE_NORMAL,"全ての処理を正常に終了しました。"
	AddDebugConsole STYLE_STATUS,"終了待機中"
	#ifndef _debug
	gosub*ExportLog
	#endif
	return

*Exit
	gosub*ExitProg
	end