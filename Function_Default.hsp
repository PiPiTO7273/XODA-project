
//エラー回避
goto*Startup

#module
	#uselib "kernel32.dll"
	#func VirtualProtect "VirtualProtect" int, int, int, int
#deffunc getdouble
	if code == 0 {
		code=$0424448b,$04c218dd,$00000000
		VirtualProtect varptr(code),length(code)*4,$40,varptr(res)
		fret=0.0
	}
	prm=varptr(fret)
	res=callfunc(prm,varptr(code),1)
	return fret

	#defcfunc ConvUTF8toANSI str _p
		sdim tmpstr,512
		cnvstoa tmpstr,_p
		return tmpstr

	#defcfunc GetINIvalue str _inibuf,str _section,str _key
		sdim INIBuf:sdim NoteLine:sdim curSection:sdim resValue
		INIBuf=_inibuf

		notesel INIBuf
		repeat notemax
			noteget NoteLine,cnt
			if strmid(NoteLine,0,1)=="["&strmid(NoteLine,-1,1)=="]" {
				curSection=NoteLine
			}
			if (getpath(curSection,16)==("["+getpath(_section,16)+"]"))|curSection=="" {
				sdim INIprm:split NoteLine,"=",INIprm
				if getpath(INIprm(0),16)==getpath(_key,16) {
					resValue=INIprm(1)
					break
				}
			}
		loop

		noteunsel
		return resValue
#global

#deffunc AddDebugConsole int _style,str _msg
	sdim tmpstr
	if _style!STYLE_STATUS&_style!STYLE_COMMENT {
		tmpstr+strf("[%04d-%02d-%02d %02d:%02d:%02d.%03d], ",gettime(0),gettime(1),gettime(3),gettime(4),gettime(5),gettime(6),gettime(7))
	}
	dim tmpcolorcode
	switch _style
		case STYLE_NORMAL
			tmpcolorcode=1+2+4
			tmpstr+"[INFO],    - "
		swbreak
		case STYLE_WARNING
			tmpcolorcode=2+4
			tmpstr+"[WARNING], - "
		swbreak
		case STYLE_ERROR
			tmpcolorcode=4
			tmpstr+"[ERROR],   - "
		swbreak
		case STYLE_STATUS
			tmpcolorcode=8
			tmpstr+" - "
		swbreak
		case STYLE_COMMENT
			tmpcolorcode=1+2
		swbreak
	swend
	tmpstr+ConvUTF8toANSI(_msg)+"\n"

	//デバッグモード時のみコンソールの出力を行う
	#if DebugConsoleWindow
	console_color tmpcolorcode
	puts tmpstr
	#endif
	ConsoleLogBuf+tmpstr
	return

#defcfunc RepHiddenWords str _p,str _hiddenstr
	if SecViewFlag==TRUE :return _p
	sdim tmpstr
	repeat strlen(_p)
		tmpstr+_hiddenstr
	loop
	return tmpstr

#deffunc RegAudioLib int _type,str _filename
	if _type==TYPE_BGM :AddDebugConsole TYPE_NORMAL,"[BGM] \""+_filename+"\" の読み込み完了"
	if _type==TYPE_SE :AddDebugConsole TYPE_NORMAL,"[SE] \""+_filename+"\" の読み込み完了"
	return

#defcfunc LoadGraph str _filename
	dim htmpgraph:htmpgraph=LoadGraph_org(_filename)
	if htmpgraph==-1 {
		AddDebugConsole TYPE_ERROR,"[IMG] \""+_filename+"\" の読み込みでエラーが発生しました"
		goto*StopError
	}
	AddDebugConsole TYPE_NORMAL,"[IMG] \""+_filename+"\" の読み込み完了"
	return htmpgraph

#defcfunc ConvBooltoStr int _val
	if _val==TRUE :return"TRUE":else:return"FALSE"

//初期化処理
#deffunc ExScript_Init str _path,str _symbol

	AddDebugConsole STYLE_NORMAL,"[EXD] \""+_path+"\" 読み込みを開始"

	dim exdBGhwnd		//背景のハンドル
	dim exdImgHwnd		//イメージのハンドル格納
	dim exdImgMax		//イメージの最大数
	dim exdFuncHWND		//画像ハンドル
	dim exdFuncAlpha		//不透明度
	dim exdFuncWidth		//横幅
	dim exdFuncHeight		//高さ
	dim exdFuncMargin		//間隔
	dim exdFuncPosY		//位置(y)
	dim exdFuncSpeed		//速度
	dim exdFuncMax		//描画する回数
	dim exdVisColor,3	//ビジュアライザーの色

	//ファイル読み込み
	exist _path
	if strsize==-1 {
		AddDebugConsole STYLE_ERROR,"外部のスクリプトファイルが見つかりませんでした。"
		goto*StopError
	}
	dim exdBufSize
	exdBufSize=strsize
	sdim exdBuf,exdBufSize
	bload _path,exdBuf
	notesel exdBuf

	strrep exdBuf,"\t",""	//タブ除去

	sdim NoteLine
	sdim FuncName:sdim FuncPrm:sdim FuncDivPrm
	sdim CurSymbol
	repeat notemax
		noteget NoteLine,cnt

		//セクション開始
		if strmid(NoteLine,-1,1)=="{" {
			strrep NoteLine,"{",""
			strrep NoteLine," ",""
			CurSymbol=NoteLine
			continue
		}
		//セクション終了
		if NoteLine=="}" {
			CurSymbol=""
			continue
		}

		//コマンド行
		if getpath(CurSymbol,16)==getpath(_symbol,16) {
			split NoteLine," ",FuncName,FuncPrm
			split FuncPrm,",",FuncDivPrm

			switch FuncName
				//背景を登録
				case"RegBackground"
					exdBGhwnd=LoadGraph("Data/Object/Background/"+FuncPrm)
				swbreak
				//画像ファイルを新規登録
				case"RegImage"
					if exdImgMax!int(FuncDivPrm(0)) {
						AddDebugConsole STYLE_WARNING,"[EXD] 画像ファイルの登録順が変更されました。"
						AddDebugConsole STYLE_STATUS,"ID["+int(FuncDivPrm(0))+"] > ID["+exdImgMax+"]"
					}
					exdImgHwnd(exdImgMax)=LoadGraph("Data/Object/Background/"+FuncDivPrm(1))
					if exdImgHwnd(exdImgMax)==0 {
						AddDebugConsole STYLE_WARNING,"[EXD] 読み込みエラー \"Data/Object/Background/"+FuncDivPrm(1)+"\""
					}
					exdImgMax++
				swbreak
				//オブジェクトを描画
				case"DrawObject"
					exdFuncHWND(exdFuncMax)=exdImgHwnd(int(FuncDivPrm(0)))
					exdFuncAlpha(exdFuncMax)=int(FuncDivPrm(1))
					exdFuncWidth(exdFuncMax)=int(FuncDivPrm(2))
					exdFuncHeight(exdFuncMax)=int(FuncDivPrm(3))
					exdFuncMargin(exdFuncMax)=int(FuncDivPrm(4))
					exdFuncPosY(exdFuncMax)=int(FuncDivPrm(5))
					exdFuncSpeed(exdFuncMax)=int(FuncDivPrm(6))
					exdFuncMax++
				swbreak
				//ビジュアライザーの色を指定
				case"SetVisualizerColor"
					if getpath(FuncPrm,16)=="all" {
						//虹色
						exdVisColor=-1,-1,-1
					} else {
						//単色
						exdVisColor=int(FuncDivPrm(0)),int(FuncDivPrm(1)),int(FuncDivPrm(2))
					}
				swbreak

				//例外
				default
					AddDebugConsole STYLE_WARNING,"[EXD] 無効なパラメータが指定されました。 \""+FuncName+"\""
				swbreak
			swend
		}
	loop

	AddDebugConsole STYLE_NORMAL,"[EXD] \""+_path+"\" 読み込みを完了"

	return

//終了処理
//（！）メモリリークが起きる可能性があるので、処理が完了した後に必ず呼び出すこと
#deffunc ExScript_Free

	AddDebugConsole STYLE_NORMAL,"[EXD] バッファの解放完了"
	DeleteGraph exdBGhwnd
	repeat exdImgMax
		DeleteGraph exdImgHwnd(cnt)
	loop

	return

#deffunc CreateUpdateRequest int _id
	netdlname updatepath(_id)
	neturl getpath(updatelink(_id),32)
	sdim infobuf
	netfileinfo infobuf,getpath(updatelink(_id),8)
	if stat!0|infobuf=="" {
		AddDebugConsole STYLE_ERROR,"HTTPリクエストの作成に失敗しました。 [ID="+_id+"]"
		return -1
	}
	AddDebugConsole STYLE_NORMAL,"HTTPリクエストを発行しました。 [ID="+_id+"]"
	AddDebugConsole STYLE_STATUS,infobuf
	notesel infobuf
	repeat notemax
		noteget NoteLine,cnt
		if instr(NoteLine,0,"Content-Length:")!-1 {
			strrep NoteLine,"Content-Length:",""
			updatelength=int(NoteLine)
			break
		}
	loop
	netrequest getpath(updatelink(_id),8)

	AddDebugConsole STYLE_NORMAL,"アップデートのリクエストを確認 [ID="+_id+",LENGTH="+updatelength+"]"

	return

#deffunc PlaySE int _h
	BASS_ChannelSetPosition _h,0,0,0
	BASS_ChannelPlay _h
	return

#deffunc LoadSE int _id,str _path,double _vol
	hsmpsnd(_id)=BASS_SampleLoad(FALSE,_path,0,0,0,16,0)
	hbasssnd(_id)=BASS_SampleGetChannel(hsmpsnd(_id),FALSE)
	RegAudioLib TYPE_SE,_path
	BASS_ChannelSetAttribute hbasssnd(_id),BASS_ATTRIB_VOL,tofloat(_vol)

	return

#deffunc DrawDebugString int _posx,int _posy,str _string
	SetDrawScreen DX_SCREEN_BACK
	SetDrawBlendMode DX_BLENDMODE_ALPHA,200
	DrawBox _posx,_posy,_posx+GetDrawStringWidthToHandle(_string,strlen(_string),hdxfont(fSystem)),_posy+16,GetColor(0,0,0),TRUE
	SetDrawBlendMode DX_BLENDMODE_NOBLEND,256
	DrawStringToHandle _posx,_posy,_string,GetColor(255,255,255),hdxfont(fSystem)
	return

#defcfunc GetDifficultyColor str _difficulty

	if _difficulty=="NORMAL" :return GetColor(83,208,255)
	if _difficulty=="HARD" :return GetColor(255,221,83)
	if _difficulty=="CHAOS" :return GetColor(255,83,249)

	return GetColor(200,200,200)

#defcfunc GetJudgeColor int _judge

	switch _judge
		case JUDGE_RES_MISS:return GetColor(70,70,70):swbreak
		case JUDGE_RES_BAD:return GetColor(145,0,255):swbreak
		case JUDGE_RES_NICE:return GetColor(188,255,0):swbreak
		case JUDGE_RES_GREAT:return GetColor(255,170,0):swbreak
		case JUDGE_RES_EXCELLENT:return GetColor(255,0,161):swbreak
	swend

	return 0

#deffunc WebAPI_Init int _windowid
	WebAPIControl_WindowID=_windowid
	if HTTPhostWindow==1 :screen _windowid,640,480,0:else:screen _windowid,640,480,2
	title"Portal WebAPI Control"
	font"Meiryo UI",14:objmode 2
	objsize 50,25
	pos 0,0:button gosub"<-",*WebAPIControl_Back
	pos 50,0:button gosub"->",*WebAPIControl_Forward
	objsize 60,25
	pos 580,0:button gosub"Run",*WebAPIControl_Run
	sdim WebAPIControl_InputURL
	pos 100,0:input WebAPIControl_InputURL,480,25,0
	pos 0,25:axobj IEobj,"Shell.Explorer.2",640,465
	comevent IEevent,IEobj,"{34A715A0-6587-11D0-924A-0020AFC7AC4D}",*WebAPIControl_OnEvent
	AddDebugConsole STYLE_NORMAL,"WebAPIの実行環境を初期化しました。"
	return
#defcfunc WebAPI_PostandGet str _url,int _windowid
	AddDebugConsole STYLE_NORMAL,"HTTPリクエストを発行しました \""+_url+"\""
	IEobj->"Navigate" _url
	AddDebugConsole STYLE_NORMAL,"HTTPリクエストを待機中... \""+_url+"\""
	repeat:wait:if IEobj("Busy")=0 {break}:loop	//待機
	AddDebugConsole STYLE_NORMAL,"HTTPリクエストを受信しました \""+_url+"\""
	IEmes=IEobj("Document")
	IEmes=IEmes("body")
	IErawdata=IEmes("innerHTML")
	strrep IErawdata,"<BR>","\n"
	return IErawdata
#deffunc WebAPI_Post str _url,int _windowid
	AddDebugConsole STYLE_NORMAL,"HTTPリクエストを発行しました \""+_url+"\""
	IEobj->"Navigate" _url
	return
#defcfunc WebAPI_Get
	AddDebugConsole STYLE_NORMAL,"HTTPリクエストを待機中... \""+_url+"\""
	repeat:wait 10:if IEobj("Busy")=0 {break}:loop	//待機
	IEmes=IEobj("Document")
	IEmes=IEmes("body")
	IErawdata=IEmes("innerHTML")
	strrep IErawdata,"<BR>","\n"
	AddDebugConsole STYLE_NORMAL,"HTTPリクエストを受信しました \""+_url+"\""
	return IErawdata

*WebAPIControl_Back
	IEobj->"GoBack"
	return
*WebAPIControl_Forward
	IEobj->"GoForward"
	return
*WebAPIControl_Run
	IEobj->"Navigate" WebAPIControl_InputURL
	return
*WebAPIControl_OnEvent
	if comevdisp(IEevent)==259 {
		comevarg WebAPIControl_InputURL,IEevent,1
		//オブジェクト反映
			defActiveWindowID=ginfo(3)
			gsel WebAPIControl_WindowID
			objprm 3,WebAPIControl_InputURL
			gsel defActiveWindowID
	}
	return

*ExportLog
	notesel ConsoleLogBuf
	notesave"Log.txt"
	return

*InitScene

	//シーン別カウンタ
	dim SceneCount

	//イージング関数用
	dim EasePosX
	dim EasePosY
	dim EaseWidth
	dim EaseHeight
	dim EaseAlpha

	randomize

	AddDebugConsole STYLE_NORMAL,"シーンの初期化完了"

	return

*OnlineSignin

	//オンライン認証
	sdim authUserName
	sdim authPassword

	exist"Data/core_OnlineAuth.dat"
	if strsize==-1|OfflineModeFlag==TRUE {
		if strsize==-1 :AddDebugConsole STYLE_NORMAL,"アカウント情報が見つかりません。ゲストユーザーで開始します。"
		if OfflineModeFlag==TRUE :AddDebugConsole STYLE_NORMAL,"オフラインモードで実行中です。ゲストユーザーで開始します。"
		authUserName="GUEST"
		authPassword=""
		gosub*SetGuestSavedata
	} else {
		AddDebugConsole STYLE_NORMAL,"オンライン認証を開始します。"
		sdim authInfobuf,strsize
		notesel authInfobuf
		noteload"Data/core_OnlineAuth.dat"
		authUserName=GetINIvalue(authInfobuf,"ONLINEAUTH","ID")
		authPassword=GetINIvalue(authInfobuf,"ONLINEAUTH","KEY")

		//FTP接続
		;ftpopen SERVICE_HOST,SERVICE_USERNAME,SERVICE_PASSWORD
		;sdim curFTPdir:ftpdir curFTPdir,"svcsavedata"

		//HTTP接続
			sdim saveInfobuf
			saveInfobuf=WebAPI_PostandGet(SERVICE_HTTP+"/API-GetProfile.php?key="+APIkey_GetProfile+"&decode="+SERVICE_ENCKEY+"&id="+authUserName+"&password="+authPassword,1)
			notesel saveInfobuf
			//エラー判別
			dim onlineErrorFlag
			switch saveInfobuf
				case"ERROR_UNKNOWN_PRM"
					AddDebugConsole STYLE_ERROR,"Portal WebAPI: 無効なパラメータが指定されました。"
					onlineErrorFlag=TRUE
				swbreak
				case"ERROR_AUTH_PASSWORD"
					AddDebugConsole STYLE_ERROR,"Portal WebAPI: パスワードの認証に失敗しました。"
					onlineErrorFlag=TRUE
				swbreak
				case"ERROR_AUTH_APIKEY"
					AddDebugConsole STYLE_ERROR,"Portal WebAPI: APIキーの認証に失敗しました。"
					onlineErrorFlag=TRUE
				swbreak
			swend

		if onlineErrorFlag==FALSE {
			//変数初期化
				sdim aqName
				sdim aqPassword
				sdim aqBadge
				sdim aqRate
				dim aqLevel
				sdim aqLastplay
				dim aqNumClear
				dim aqNumFullcombo
				dim aqNumAllEx
				dim aqNumTicket
				sdim aqHistory

				//プレイオプション
				dim aqPlayOption,9

				//初回登録
				if instr(saveInfobuf,0,"XODA.RATE")==-1 :saveInfobuf+"XODA.RATE=0.0\n"
				if instr(saveInfobuf,0,"XODA.LEVEL")==-1 :saveInfobuf+"XODA.LEVEL=0\n"
				if instr(saveInfobuf,0,"XODA.BADGE")==-1 :saveInfobuf+"XODA.BADGE=\n"
				if instr(saveInfobuf,0,"XODA.LASTPLAY")==-1 :saveInfobuf+"XODA.LASTPLAY=-\n"
				if instr(saveInfobuf,0,"XODA.NUMCLEAR")==-1 :saveInfobuf+"XODA.NUMCLEAR=\n"
				if instr(saveInfobuf,0,"XODA.NUMFULLCOMBO")==-1 :saveInfobuf+"XODA.NUMFULLCOMBO=\n"
				if instr(saveInfobuf,0,"XODA.NUMALLEX")==-1 :saveInfobuf+"XODA.NUMALLEX=\n"
				if instr(saveInfobuf,0,"XODA.NUMTICKET")==-1 :saveInfobuf+"XODA.NUMTICKET=\n"
				if instr(saveInfobuf,0,"XODA.HISTORY")==-1 :saveInfobuf+"XODA.HISTORY=++++\n"
				if instr(saveInfobuf,0,"XODA.PARTNER.SELECT")==-1 :saveInfobuf+"XODA.PARTNER.SELECT={empty}\n"

				repeat 9
					if instr(saveInfobuf,0,"XODA.PLAYOPTION."+cnt)==-1 :saveInfobuf+"XODA.PLAYOPTION."+cnt+"="+Item_DefaultOptionValue(cnt)+"\n"
				loop

				aqName=GetINIvalue(saveInfobuf,"SAVEDATA","USERNAME")						//ユーザー名
				aqRate=double(GetINIvalue(saveInfobuf,"SAVEDATA","XODA.RATE"))				//レート
				aqLevel=int(GetINIvalue(saveInfobuf,"SAVEDATA","XODA.LEVEL"))				//レベル
				aqBadge=GetINIvalue(saveInfobuf,"SAVEDATA","XODA.BADGE")					//称号
				aqLastplay=GetINIvalue(saveInfobuf,"SAVEDATA","XODA.LASTPLAY")				//最後にプレイした日付
				aqNumClear=int(GetINIvalue(saveInfobuf,"SAVEDATA","XODA.NUMCLEAR"))			//クリア数
				aqNumFullcombo=int(GetINIvalue(saveInfobuf,"SAVEDATA","XODA.NUMFULLCOMBO"))	//フルコンボ数
				aqNumAllEx=int(GetINIvalue(saveInfobuf,"SAVEDATA","XODA.NUMALLEX"))			//ALLEX数
				aqNumTicket=int(GetINIvalue(saveInfobuf,"SAVEDATA","XODA.NUMTICKET"))			//チケットの数

				repeat 9
					aqPlayOption(cnt)=int(GetINIvalue(saveInfobuf,"SAVEDATA","XODA.PLAYOPTION."+cnt))
				loop

				//パートナー情報
					sdim pnCurSelect:dim pnCurID
					pnCurSelect="{empty}"
					pnCurSelect=GetINIvalue(saveInfobuf,"SAVEDATA","XODA.PARTNER.SELECT")
					gosub*LoadSkillData
					repeat pnMax
						sdim tmpstr
						tmpstr=GetINIvalue(saveInfobuf,"SAVEDATA","XODA.PARTNER."+pnID(cnt))
						sdim tmp_pnlev:sdim tmp_pnexp
						split tmpstr,",",tmp_pnlev,tmp_pnexp
						pnLevel(cnt)=int(tmp_pnlev)
						pnExp(cnt)=int(tmp_pnexp)

						if pnCurSelect==pnID(cnt) :pnCurID=cnt
					loop

				//プレイ履歴
					sdim tmpstr:tmpstr=GetINIvalue(saveInfobuf,"SAVEDATA","XODA.HISTORY")
					split tmpstr,"+",aqHistory
		} else {
			AddDebugConsole STYLE_WARNING,"オンライン認証に失敗しました。ゲストユーザーで開始します。"
			gosub*SetGuestSavedata
		}

	}
	AddDebugConsole STYLE_NORMAL,"Portalサービスにサインインしました。"
	AddDebugConsole STYLE_STATUS,"USERNAME(ID) = "+authUserName
	AddDebugConsole STYLE_STATUS,"PASSWORD(KEY) = "+authPassword
	return

*SetGuestSavedata
	authUserName="GUEST"
	authPassword=""

	aqName=authUserName
	aqRate=0.0
	aqLevel=0
	aqBadge="ゲスト"
	aqLastplay="-"
	aqNumClear=0
	aqNumFullcombo=0
	aqNumAllEx=0
	aqNumTicket=0
	aqHistory="","","","",""
	pnCurSelect="{empty}"
	pnCurID=0

	gosub*LoadSkillData

	return

*OnlineUpdate

	AddDebugConsole STYLE_NORMAL,"オンラインアップデートを実行します。"
	gsel 0,1

	//開始
		dim htmpFont
		htmpFont(0)=CreateFontToHandle("Meiryo UI",20,-1,DX_FONTTYPE_ANTIALIASING_4X4)
		htmpFont(1)=CreateFontToHandle("Meiryo UI",16,-1,DX_FONTTYPE_ANTIALIASING_4X4)

		dim updateID:dim updateres:dim updatesize:dim updatelength:dim updatesuc

		netinit

		repeat
			if updateflag(updateID)==FALSE :updateID++:else:break
		loop
		CreateUpdateRequest updateID
		if stat==-1 :OfflineModeFlag=TRUE
		repeat

			//オフラインモードの確認
			if OfflineModeFlag==TRUE :break

				exist updatepath(updateID)
				updatesize=strsize

				SetDrawScreen DX_SCREEN_BACK
				ClearDrawScreen

				//画面初期化
				DrawBox 0,0,DispWidth,DispHeight,GetColor(255,255,255),TRUE
				DrawBox 0,0,DispWidth,128,GetColor(245,245,245),TRUE
				DrawLine 0,128,DispWidth,128,GetColor(200,200,200)

				DrawStringToHandle 64,80,"オンラインアップデート",GetColor(25,25,25),htmpFont(0)
				DrawStringToHandle 1130,85,"Ver "+BuildVersion_Major+"."+BuildVersion_Minor,GetColor(50,50,50),htmpFont(1)

				DrawStringXCenterToHandle 0,240,"アップデートに必要なファイルをダウンロード中です。\nしばらくお待ち下さい...",DispWidth,GetColor(40,40,40),htmpFont(1)

				DrawStringToHandle 265,360,updatepath(updateID),GetColor(50,50,50),htmpFont(1)
				DrawStringXAlignRightToHandle 265,360,""+(updatesuc+1)+"/"+updatemax+"件のダウンロード",750,GetColor(50,50,50),htmpFont(1)
				DrawBox 265,400,265+750,400+20,GetColor(150,150,150),FALSE
				DrawBox 265,400,265+int(750.0*(double(updatesize)/double(updatelength))),400+20,GetColor(184,255,41),TRUE
				DrawBox 265,400,265+int(750.0*(double(updatesize)/double(updatelength))),400+20,GetColor(150,150,150),FALSE

				ScreenFlip

			netexec updateres
			if updateres!0 {
				repeat
					updateID++
					if updateID>=length(updateflag) :break
					if updateflag(updateID)==FALSE :updateID++:else:break
				loop
				if updateID>=length(updatepath) :break
				CreateUpdateRequest updateID
				if stat==-1 :OfflineModeFlag=TRUE
				updatesuc++
			}
			await
		loop

		DeleteFontToHandle htmpFont(0)
		DeleteFontToHandle htmpFont(1)
	return

*Create_AQProfileBuf

	/*
	alCreateImage i2D_Profile,300,120
		alColor 230,230,230,255
		alFont"Pier Sans",20	//名前
		alDrawText aqName,120,14,300-120,120-14,0,0
		alFont"Pier Sans",16	//レート
		alDrawText "RATE:"+aqRate,120,47,300-120,120-47,0,0
		alFont"Noto Sans CJK JP Regular",14	//称号
		alDrawText aqBadge,120,70,300-120,120-70,0,0

	if hdximg(iUI_tx_Profile)!0 :DeleteGraph hdximg(iUI_tx_Profile)
	hdximg(iUI_tx_Profile)=MakeScreen(300,120,TRUE)
	alCopyImageToDXBuf i2D_Profile,hdximg(iUI_tx_Profile)
	*/

	if hdximg(iUI_tx_Profile)!0 :DeleteGraph hdximg(iUI_tx_Profile)
	hdximg(iUI_tx_Profile)=MakeScreen(300,120,TRUE)
		SetDrawScreen hdximg(iUI_tx_Profile)
		SetDrawBlendMode DX_BLENDMODE_ALPHA,256
		ChangeFontType DX_FONTTYPE_ANTIALIASING_4X4
		ChangeFont"Pier Sans"
		SetFontSize 20:DrawString 120,14,aqName,GetColor(230,230,230)
		SetFontSize 16:DrawString 120,47,"RATE:"+strf("%.2f",aqRate),GetColor(230,230,230)
		ChangeFont"Noto Sans CJK JP Regular"
		SetFontSize 14:DrawString 120,70,aqBadge,GetColor(230,230,230)

	AddDebugConsole STYLE_NORMAL,"アカウント情報が更新されました。"
	AddDebugConsole STYLE_STATUS,"NAME = "+aqName
	AddDebugConsole STYLE_STATUS,"RATE = "+aqRate
	AddDebugConsole STYLE_STATUS,"BADGE = "+aqBadge
	AddDebugConsole STYLE_STATUS,"HANDLE = "+hdximg(iUI_tx_Profile)
	return

*LoadDetailRecord

	//詳細記録を読み込み
	sdim DetailRecordBuf
		//URL生成
		sdim tmpstr
		tmpstr=SERVICE_HTTP+"/API-GetRecord.php?key="+APIkey_GetRecord
		tmpstr+"&name="+aqName
		strrep tmpstr," ","%20"
	DetailRecordBuf=WebAPI_PostandGet(tmpstr)
	return

*SyncIR

	if OfflineModeFlag==FALSE&IRsyncFlag==FALSE {
		if IEobj("Busy")==0 {
			AddDebugConsole STYLE_NORMAL,"HTTPリクエストを取得しました。"
			sdim IRsavedatabuf,4096
			notesel IRsavedatabuf
			IEmes=IEobj("Document")
			IEmes=IEmes("body")
			IErawdata=IEmes("innerHTML")
			strrep IErawdata,"<BR>","\n"
			IRsavedatabuf=IErawdata

			//エラー判別
			dim IRerrorFlag
			switch IRsavedatabuf
				case"ERROR_ACCESS_SHARED"
					AddDebugConsole STYLE_WARNING,"Portal WebAPI: 共有セーブデータが見つかりませんでした。"
					IRerrorFlag=TRUE
				swbreak
				case"ERROR_AUTH_APIKEY"
					AddDebugConsole STYLE_ERROR,"Portal WebAPI: APIキーの認証に失敗しました。"
					goto*StopError
				swbreak
			swend

			if IRerrorFlag==FALSE {
				repeat notemax
					noteget NoteLine,cnt
					if cnt==0 :continue		//セクション行は無視

					split NoteLine,"=",INIkey,INIval
					split INIkey,".",TargetIRchartID,TargetIRrank
					strrep TargetIRrank,"RANK#",""
					split INIval,",",TargetIRname,TargetIRscore

					dim TargetArrLevID:TargetArrLevID=-1
					dim TargetArrSelID:TargetArrSelID=-1
					dim CurArrID

					repeat Libarr_Max
						CurArrID=cnt
						repeat 3
							if TargetIRchartID==Libarr_ChartID(cnt,CurArrID) {
								TargetArrLevID=cnt
								TargetArrSelID=CurArrID
							}
						loop
					loop
					if TargetArrSelID==-1|TargetArrLevID==-1 {
						AddDebugConsole STYLE_WARNING,"[SHARED] 無効なスコアデータです。"
					} else {
						TargetIRrank=int(TargetIRrank)-1
						Libarr_IRrank(TargetArrLevID,TargetArrSelID,TargetIRrank)=int(TargetIRrank)
						Libarr_IRname(TargetArrLevID,TargetArrSelID,TargetIRrank)=TargetIRname
						Libarr_IRscore(TargetArrLevID,TargetArrSelID,TargetIRrank)=int(TargetIRscore)
						AddDebugConsole STYLE_NORMAL,"ランキングスコアを読み込みました。["+TargetArrLevID+","+TargetArrSelID+","+TargetIRrank+"]"
						if TargetIRname==aqName {
							Libarr_MyIRrank(TargetArrLevID,TargetArrSelID)=int(TargetIRrank)
							Libarr_MyIRscore(TargetArrLevID,TargetArrSelID)=int(TargetIRscore)
						}
					}
				loop
			}

			IRsyncFlag=TRUE
		} else {
			IRsyncFrame++
		}
	}

	return